import "utils/block";
import "utils/plot";

function loc.getHighestPoint(loc) {
	y = 319;
	loc.setY(loc, y);
	while(y > 0 && block.isAir(block.get(loc))) {
		loc.setY(loc, y);
		y = y - 10;
	}
	while(!block.isAir(block.get(loc))) {
		loc.setY(loc, y);
		y++;
	}
	return loc;
}

function loc.getStringColon(loc) {
	world_name = world.getName(loc.getWorld(loc));
	x = string.number(loc.getX(loc));
	y = string.number(loc.getY(loc));
	z = string.number(loc.getZ(loc));
	return string.concat(world_name, ":", x, ":", y, ":", z);
}

function loc.getStringSpace(loc) {
	world_name = world.getName(loc.getWorld(loc));
	x = string.number(loc.getX(loc));
	y = string.number(loc.getY(loc));
	z = string.number(loc.getZ(loc));
	return string.concat(world_name, " ", x, " ", y, " ", z);
}

function loc.getStringRounded(loc, separator) {
	x = string.number(math.round(loc.getX(loc)));
	y = string.number(math.round(loc.getY(loc)));
	z = string.number(math.round(loc.getZ(loc)));
	return string.concat(separator, x, separator, y, separator, z);
}

function loc.isSameWorld(loc1, loc2) {
	return world.getName(loc.getWorld(loc1)) == world.getName(loc.getWorld(loc2));
}

function loc.getSubPlot(location) {
	list = plot.get(location);
	iter = iterator(list);
	while(hasNext(iter)) {
		plot = next(iter);
		if(plot.isSub(plot)) {
			return plot;
		}
	}
	return null;
}

function loc.isCity(location) {
	list = plot.get(location);
	iter = iterator(list);
	while(hasNext(iter)) {
		plot = next(iter);
		if(plot.isCity(plot)) {
			return true;
		}
	}
	return false;
}

function loc.hasPlotId(location, plot_id) {
	return list.contains(plot.getIdListFromLocation(location), plot_id);
}

function loc.hasPlotName(location, plot_name) {
	list = plot.get(location);
	iter = iterator(list);
	while(hasNext(iter)) {
		plot = next(iter);
		if(plot.getName(plot) == plot_name) {
			return true;
		}
	}
	return false;
}

function loc.isInSurvWorld(location) {
	world_name = world.getName(loc.getWorld(location));
	if(world_name == "world") {
		return true;
	} elseif(world_name == "world_the_end") {
		return true;
	} elseif(world_name == "world_nether") {
		return true;
	}
	return false;
}

function loc.isInGamesWorld(location) {
	return loc.getWorld(location) == world.getGames();
}

function loc.isInCreativeWorld(location) {
	return loc.getWorld(location) == world.getCreative();
}

function loc.round(location) {
	world = loc.getWorld(location);
	x = math.round(loc.getX(location));
	y = math.round(loc.getY(location));
	z = math.round(loc.getZ(location));
	return loc.new(world, x, y, z);
}

function getDirection(yaw) {
	while(yaw < 0) {
		yaw += 360;
	}
	while(yaw >= 360) {
		yaw -= 360;
	}
	if(yaw > 60 && yaw < 120) {
		direction = "west";
	} elseif(yaw > 150 && yaw < 210) {
		direction = "north";
	} elseif(yaw > 240 && yaw < 300) {
		direction = "east";
	} elseif(yaw > 330 || yaw < 30) {
		direction = "south";
	} else {
		return null;
	}
	return direction;
}

function getDirectionExact(yaw) {
	while(yaw < 0) {
		yaw += 360;
	}
	while(yaw >= 360) {
		yaw -= 360;
	}
	if(yaw > 67.5 && yaw <= 112.5) {
		direction = "West";
	} elseif(yaw > 112.5 && yaw <= 157.5) {
		direction = "North-West";
	} elseif(yaw > 157.5 && yaw <= 202.5) {
		direction = "North";
	} elseif(yaw > 202.5 && yaw <= 247.5) {
		direction = "North-East";
	} elseif(yaw > 247.5 && yaw <= 292.5) {
		direction = "East";
	} elseif(yaw > 292.5 && yaw <= 337.5) {
		direction = "South-East";
	}elseif(yaw > 337.5 || yaw <= 22.5) {
		direction = "South";
	} elseif(yaw > 22.5 && yaw <= 67.5) {
		direction = "South-West";
	}else {
		return null;
	}
	return direction;
}

function getBlockLocation(location) {
	world = loc.getWorld(location);
	bx = loc.getBlockX(location);
	by = loc.getBlockY(location);
	bz = loc.getBlockZ(location);
	return loc.new(world, bx, by, bz);
}