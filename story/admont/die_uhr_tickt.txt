event.load("quest_start");
event.load("quest_term");
event.load("entity_click");
event.load("block_break");

stage = 0;
all_stages = 4;
quest_name = "Die Uhr tickt";

gold = 0;
orchids = 0;
bluedye = 0;
gold_ingot_needed = 5;

@wait
wait();
if(!player.isQuester(player, script)) {
	goto("wait");
}
if(event == "quest_term") {
	//Hier Code einfügen...
	wait(); //Script wird von außen terminiert
}
if(event == "quest_start") {
	quest.display(player, quest_name, stage, all_stages);
	msg.prefix(player, "§dSchmied", "Guten Tag!");
	scheduler.msgPrefix(30, player, "§dSchmied", "Der gute Herbert hat eine neue Uhr in Auftrag gegeben.");
	scheduler.msgPrefix(60, player, "§dSchmied", "Ich wollte gerade anfangen, da habe ich bemerkt, dass er mir noch garnicht das Gold dafür geliefert hat.");
	scheduler.msgPrefix(90, player, "§dSchmied", "Sei doch so lieb und schau' beim Herbert vorbei, hole 5 Goldstücke und richte ihm meine Grüße aus.");
	goto("wait");
}

label = concat("stage", text.number(stage));
goto(label);

@stage0
if(event == "entity_click" && hand == "MAIN_HAND" && isCitizen(entity)) {
	entity_name = entity.getName(entity);
	string_name = string.text(entity_name);
	if(string_name == "Herbert") {
		stage.increase(player);
		msg.prefix(player, "§dHerbert", "Naa, was führt dich hierher?");
		scheduler.msgPrefix(30, player, concat("§5", player.getName(player)), "Ich soll Grüße von der Schmiede ausrichten, es werden 5 Goldstücke für deine Uhr benötigt.");
		scheduler.msgPrefix(60, player, "§dHerbert", "Ooohh das habe ich ja glatt vergessen. Hier!");
		scheduler.giveItem(80, player, read.item("minecraft:gold_ingot", 5));
		scheduler.msgPrefix(90, player, "§dHerbert", "Bitte bring sie direkt zur Schmiede! Ich brauche diese Uhr.");
	}
}
goto("wait");

@stage1
if(event == "entity_click" && hand == "MAIN_HAND" && isCitizen(entity)) {
	entity_name = entity.getName(entity);
	string_name = string.text(entity_name);
	if(string_name == "Schmied") {
		item = living.getEquip(player, "hand");
		item_type = item.getType(item);
		rest_amount = citizen.giveItem(string_name, player, "minecraft:gold_ingot", gold_ingot_needed);
		if(rest_amount == 0) {
			msg.prefix(player, "§dSchmied", "Perfekt, jetzt fehlt nur noch hellblauer Farbstoff für das Detail.");
			scheduler.msgPrefix(30, player, "§dSchmied", "Pflücke eine blaue Orchidee und verarbeite sie zu Farbstoff.");
			stage.increase(player);
		} else {
			gold_ingot_needed = rest_amount;
		}
	}
}	
goto("wait");

@stage2
if(event == "block_break") {
	if(block_type == "minecraft:blue_orchid") {
		cancel = false;
		scheduler.setBlock(5, block_loc, "minecraft:blue_orchid", false);
		orchids++;
		if(orchids == 1) {
			stage.increase(player);
			msg.prefix(player, "§dSchmied", "Gut, nun mache Farbstoff daraus.");
		}
	}
}
goto("wait");

@stage3
if(event == "entity_click" && hand == "MAIN_HAND" && isCitizen(entity)) {
	entity_name = entity.getName(entity);
	string_name = string.text(entity_name);
	if(string_name == "Schmied") {
		rest_amount = citizen.giveItem(string_name, player, "minecraft:light_blue_dye", 1);
		if(rest_amount == 0) {
			stage.increase(player);
			msg.prefix(player, "§dSchmied", "Jetzt kann ich loslegen!");
			scheduler.msgPrefix(60, player, "§dSchmied", "Einen Moment noch.");
			scheduler.msgPrefix(120, player, "§dSchmied", "Tadaaaa. Das Werk ist vollbracht. Du kannst sie ihm gleich vorbeibringen.");
			scheduler.giveSingleItem(120, player, read.item("minecraft:clock"), false);
		}
	}
}
goto("wait");

@stage4
if(event == "entity_click" && hand == "MAIN_HAND" && isCitizen(entity)) {
	entity_name = entity.getName(entity);
	string_name = string.text(entity_name);
	if(string_name == "Herbert") {
		rest_amount = citizen.giveItem(string_name, player, "minecraft:clock", 1);
		if(rest_amount == 0) {
			msg.prefix(player, "§dHerbert", "Das ging aber flott!");
			msg(player, "§dQuest abgeschlossen. Belohnung: 22 Snuvis!");
			money.addBoost(player, 22);
			quest.finish(script, player);
		}
	}
}
goto("wait");