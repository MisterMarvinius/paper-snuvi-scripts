event.load("quest_start");
event.load("quest_term");
event.load("block_drop");
event.load("block_click");
event.load("block_break");
event.load("entity_click");

stage = 0;
all_stages = 3;
quest_name = "Harvest Season";

wheat_needed = 15;
carrots_needed = 8;
wheat_harvestet = 0;
carrots_harvestet = 0;
wheat_mat = material.get("WHEAT");
carrot_mat_block = material.get("CARROTS");
carrot_mat_item = material.get("CARROT");

@wait
wait();
if(!player.isQuester(player, script)) {
	goto("wait");
}
if(event == "quest_term") {
	//Hier Code einfügen...
	wait(); //Script wird von außen terminiert
}
if(event == "quest_start") {
	quest.display(player, quest_name, stage, all_stages);
	msg.prefix(player, "§dFarmer", "Good of you to look in. Why don't you help me with the harvest and get me 15 wheat from the field?.");
	scheduler.msgPrefix(30, player, "§dFarmer", "When you're done with that, I'd need 8 more carrots.");
	goto("wait");
}
label = string.concat("stage", string.number(stage));
goto(label);

@stage0
@stage1
@stage2
if(event == "block_drop") {
	block_type = block.getType(block);
	if(block_type == wheat_mat) {
		item_entity = list.getIndex(drops, 0);
		item.entity.set(item_entity, item.new(wheat_mat));
		list.clear(drops);
		list.add(drops, item_entity);
		goto("wait");
	}
	if(block_type == carrot_mat_block) {
		item_entity = list.getIndex(drops, 0);
		item.entity.set(item_entity, item.new(carrot_mat_item));
		list.clear(drops);
		list.add(drops, item_entity);
	}
	goto("wait");
}
if(event == "block_click") {
	if(block == null) {
		goto("wait");
	}
	block_type = block.getType(block);
	if(block_type == wheat_mat || block_type == carrot_mat_block) {
		cancel = false;
	}
	goto("wait");
}
if(event == "block_break") {
	block_type = block.getType(block);
	if(block_type == wheat_mat) {
		cancel = false;
		scheduler.setBlockData(60, block, read.blockData("minecraft:wheat[age=7]"), false);
		wheat_harvestet++;
		if(wheat_harvestet == wheat_needed) {
			stage.increase(player);
		}
	}
	if(block_type == carrot_mat_block) {
		cancel = false;
		scheduler.setBlockData(60, block, read.blockData("minecraft:carrots[age=7]"), false);
		carrots_harvestet++;
		if(carrots_harvestet == carrots_needed) {
			stage.increase(player);
		}
	}
	goto("wait");
}
if(event == "entity_click" && slot.isHand(hand) && entity.isHuman(entity)) {
	entity_name = human.getName(entity);
	if(entity_name == "Farmer") {
		item = living.getHand(player);
		item_type = item.getType(item);
		if(item_type == wheat_mat) {
			if(wheat_needed > 0) {
				rest_amount = human.giveItem(entity_name, player, item_type, wheat_needed);
				if(rest_amount == 0) {
					msg.prefix(player, "§dFarmer", "Thanks for the wheat.");
				}
				wheat_needed = rest_amount;
			}
		}
		if(item_type == carrot_mat_item) {
			if(carrots_needed > 0) {
				rest_amount = human.giveItem(entity_name, player, item_type, carrots_needed);
				if(rest_amount == 0) {
					msg.prefix(player, "§dFarmer", "Thanks for the carrots.");
				}
				carrots_needed = rest_amount;
			}
		}
		if(wheat_needed == 0 && carrots_needed == 0) {
			msg.prefix(player, "§dFarmer", "Thank you for your help!");
			msg.string(player, "§dQuest finished. Reward: 4 snuvis!");
			money.addBoost(player, 4);
			quest.finish(script, player);
		}
	}
}
goto("wait");
