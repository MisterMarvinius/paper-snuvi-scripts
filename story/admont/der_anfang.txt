event.load("entity_click");
event.load("quest_start");
event.load("quest_term");
event.load("block_click");
event.load("custom_command");

stage = 0;
all_stages = 5;
quest_name = "Der Anfang...";
cake_loc = loc.new(world.getStory(), -1511, 65, -23);

@wait
wait();
if(!player.isQuester(player, script)) {
	goto("wait");
}
if(event == "quest_term") {
	//Hier Code einfügen...
	wait(); //Script wird von außen terminiert
}
if(event == "quest_start") {
	quest.display(player, quest_name, stage, all_stages);
	msg.prefix(player, "§dOma", "Hallo Grünschnabel! Du siehst sehr verloren aus... Ist alles okay bei dir?");
	scheduler.msgPrefix(30, player, "§dOma", "Iss doch etwas von meinem frisch gebackenen Kuchen, damit du nicht verhungerst.");
	block.set(cake_loc, "minecraft:cake");
	if(player.getHunger(player) > 18) {
		player.setHunger(player, 18);
		player.setSaturation(player, 0);
	}
	goto("wait");
}
label = concat("stage", text.number(stage));
goto(label);

@stage0
if(event == "block_click") {
	if(block_loc == cake_loc) {
		stage.increase(player);
		msg.prefix(player, "§dOma", "Na, das ist doch schon besser :)");
		scheduler.msgPrefix(30, player, "§dOma", "Geh doch dort über die Brücke und sprich mit dem Gastwirt, direkt am Dorfeingang auf der rechten Seite. Er wird dir sicher behilflich sein!");
	}
}
goto("wait");

@stage1
if(event == "entity_click" && hand == "MAIN_HAND" && entity.getType(entity) == "human") {
	entity_name = entity.getName(entity);
	if(entity_name == "Gastwirt") {
		stage.increase(player);
		msg.prefix(player, "§dGastwirt", "Guten Tag, wie kann ich dir behilflich sein?");
		msg(player, "§dAntwortmöglichkeiten:");
		msg(player, text.click("[§dA§r] §eIch weiß nicht, wo ich hier gelandet bin.", "/questanswer A"));
		msg(player, text.click("[§dB§r] §eMir ist nicht mehr zu helfen.", "/questanswer B"));
	}
}
goto("wait");

@stage2
if(event == "custom_command" && command == "questanswer") {
	option = list.getIndex(args, 0);
	if(option == "A") {
		stage.increase(player);
		msg.prefix(player, "§dGastwirt", "Da bin ich der richtige Ansprechpartner. Du bist hier in Admont, einem kleinen Dörfchen in der weiten Welt.");
		scheduler.msgPrefix(30, player, "§dGastwirt", "Du siehst mir recht vertrauenswürdig aus. Vielleicht könntest du uns die eine oder andere Arbeit abnehmen.");
		scheduler.msgPrefix(60, player, "§dGastwirt", "Geh doch in mein Haus zu meiner Frau, sie wird dir weiterhelfen.");
		goto("wait");
	}
	if(option == "B") {
		stage.increase(player);
		msg.prefix(player, "§dGastwirt", "Aber hallo. Das bekommen wir schon auf die Reihe.");
		scheduler.msgPrefix(30, player, "§dGastwirt", "Du bist hier in Admont, einem kleinen Dörfchen in der weiten Welt.");
		scheduler.msgPrefix(60, player, "§dGastwirt", "Du siehst mir recht vertrauenswürdig aus. Vielleicht könntest du uns die eine oder andere Arbeit abnehmen.");
		scheduler.msgPrefix(90, player, "§dGastwirt", "Geh doch in mein Haus zu meiner Frau, sie wird dir weiterhelfen.");
	}
}
goto("wait");

@stage3
if(event == "entity_click" && hand == "MAIN_HAND" && entity.getType(entity) == "human") {
	entity_name = entity.getName(entity);
	if(entity_name == "Gastwirtin") {
		stage.increase(player);
		msg.prefix(player, "§dGastwirtin", concat("Hallo, ", player.getName(player), " du scheinst neu zu sein. Mein Mann hat mir schon von dir erzählt."));
		scheduler.msgPrefix(30, player, "§dGastwirtin", "Schau dich doch erstmal in Admont um und wenn du fertig bist, bring mir doch bitte etwas Wasser mit.");
		scheduler.msgPrefix(60, player, "§dGastwirtin", "Nach diesem langen Tag bin ich sehr durstig. Hier hast du eine Wasserflasche.");
		player.safeGiveItem(player, read.item("minecraft:glass_bottle"));
	}
}
goto("wait");

@stage4
if(event == "entity_click" && hand == "MAIN_HAND" && entity.getType(entity) == "human") {
	entity_name = entity.getName(entity);
	if(entity_name == "Gastwirtin") {
		item = living.getEquip(player, "hand");
		if(text.item(item) == "{id:\"minecraft:potion\",Count:1b,tag:{Potion:\"minecraft:water\"}}") {
			msg.prefix(player, "§dGastwirt", "Danke! Hier eine kleine Belohnung.");
			player.removeItem(player, item);
			msg(player, "§dQuest abgeschlossen. Belohnung: 22 Snuvis!");
			money.addBoost(player, 22);
			quest.finish(script, player);
		}
	}
}
goto("wait");