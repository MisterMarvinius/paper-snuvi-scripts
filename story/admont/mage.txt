event.load("entity_click");
event.load("block_click");
event.load("custom_command");
event.load("quest_start");
event.load("quest_term");
event.load("living_death");
event.load("bucket_use");
event.load("block_break");

warts_harvested = 0;
mob_killed = false;
stage = 0;
all_stages = 19;
quest_name = "Mage";

bread_mat = material.get("BREAD");
cauldron_mat = material.get("CAULDRON");
nether_wart_mat = material.get("NETHER_WART");
water_bucket_mat = material.get("water_bucket");
water_bucket = item.new(water_bucket_mat);
key = item.custom.create("GOLDEN_KEY", 1, "Mage Cave Key", "Mage");

world = world.getOverworld();
zombie_loc = loc.new(world, 215, 24, -80);
spider_loc = loc.new(world, 218, 27, -68);
witch_loc = loc.new(world, 214.5, 31, -75.5);
blaze_loc = loc.new(world, 225.5, 34, -81.5);
skeleton_loc = loc.new(world, 243.5, 29, -86.5);
illusioner_loc = loc.new(world, 231, 31, -91);

cauldron_block = block.get(loc.new(world, 216, 26, -92));
cave_spider_loc = loc.new(world, 219.5, 26, -94.5);
giveup_loc = loc.new(world, 193.5, 21, -75.5);

doors_to_close = list.new();
visited_doors = set.new();
mage_doors = set.new();
set.add(mage_doors, loc.new(world, 210, 25, -83));
set.add(mage_doors, loc.new(world, 216, 26, -75));
set.add(mage_doors, loc.new(world, 217, 31, -72));
set.add(mage_doors, loc.new(world, 217, 33, -78));
set.add(mage_doors, loc.new(world, 232, 35, -82));
set.add(mage_doors, loc.new(world, 235, 32, -88));

@wait
wait();
if(event == "living_death") {
	player = player.getFromDamageSource(damage_source);
	if(player == null) {
		goto("wait");
	}
}
if(!player.isQuester(player, script)) {
	goto("wait");
}
if(event == "quest_term") {
	player.removeItem(player, key);
	entity.teleport(player, giveup_loc);
	wait(); //Script wird von außen terminiert
}
if(event == "block_click") {
	if(block == null) {
		goto("wait");
	}
	item = living.getHand(player);
	if(item == key) {
		if(!block.isDoor(block)) {
			goto("wait");
		}
		state_open = block.isOpen(block);
		if(!block.isBisected(block)) {
			goto("wait");
		}
		state_half = block.getBisectedHalf(block);
		door_loc = block.getLocation(block);
		if(state_half == "TOP") {
			door_loc = loc.mod(door_loc, 0, -1, 0);
			block = block.get(door_loc);
		}
		if(set.contains(visited_doors, block)) {
			if(!state_open) {
				opendoor(block);
				sgoto(60, "closedoor");
			}
			goto("wait");
		}
		if(set.contains(mage_doors, door_loc)) {
			if(stage == 1) {
				stage.increase(player);
				entity.spawn(zombie_loc, "ZOMBIE");
			} elseif(stage == 3) {
				stage.increase(player);
				entity.spawn(spider_loc, "SPIDER");
			} elseif(stage == 5 && mob_killed) {
				stage.increase(player);
				entity.spawn(witch_loc, "WITCH");
			} elseif(stage == 7) {
				stage.increase(player);
				entity.spawn(blaze_loc, "BLAZE");
			} elseif(stage == 9 && mob_killed) {
				stage.increase(player);
				entity.spawn(skeleton_loc, "STRAY");
			} elseif(stage == 11 && mob_killed) {
				stage.increase(player);
				entity.spawn(illusioner_loc, "ILLUSIONER");
			} else {
				goto("wait");
			}
			if(!state_open) {
				mob_killed = false;
				opendoor(block);
				sgoto(60, "closedoor");
			}
		}
		goto("wait");
	}
}
if(event == "quest_start") {
	quest.display(player, quest_name, stage, all_stages);
	msg.prefix(player, "§dMage Guard", "The key to the mage? You can have it if you give me bread.");
	goto("wait");
}
label = string.concat("stage", string.number(stage));
ignoreGoto(label);
goto("wait");

@stage0
if(event == "entity_click" && slot.isHand(hand) && entity.isHuman(entity)) {
	entity_name = human.getName(entity);
	if(entity_name == "Mage Guard") {
		rest_amount = human.giveItem(entity_name, player, bread_mat, 1);
		if(rest_amount == 0) {
			player.giveItem(player, key);
			msg.prefix(player, "§dMage Guard", "Rightclick doors with the key to open them.");
			scheduler.msgPrefix(30, player, "§dMage Guard", "The first door is behind the tree.");
			scheduler.msgPrefix(60, player, "§dMage Guard", "Be careful!");
			stage.increase(player);
		}
	}
}
goto("wait");

@stage2
if(event == "living_death") {
	entity_type = entity.getType(living_entity);
	if(entity_type == "zombie") {
		mob_killed = true;
		stage.increase(player);
	}
}
goto("wait");

@stage4
if(event == "living_death") {
	entity_type = entity.getType(living_entity);
	if(entity_type == "spider") {
		mob_killed = true;
		stage.increase(player);
	}
}
goto("wait");

@stage6
if(event == "living_death") {
	entity_type = entity.getType(living_entity);
	if(entity_type == "witch") {
		mob_killed = true;
		stage.increase(player);
	}
}
goto("wait");

@stage8
if(event == "living_death") {
	entity_type = entity.getType(living_entity);
	if(entity_type == "blaze") {
		mob_killed = true;
		stage.increase(player);
	}
}
goto("wait");

@stage10
if(event == "living_death") {
	entity_type = entity.getType(living_entity);
	if(entity_type == "stray") {
		mob_killed = true;
		stage.increase(player);
	}
}
goto("wait");

@stage12
if(event == "living_death") {
	entity_type = entity.getType(living_entity);
	if(entity_type == "illusioner") {
		mob_killed = true;
		stage.increase(player);
	}
}
goto("wait");

@stage13
if(event == "entity_click" && slot.isHand(hand) && entity.isHuman(entity)) {
	entity_name = human.getName(entity);
	if(entity_name == "Mage Assistant") {
		item = living.getHand(player);
		if(item != key) {
			msg.prefix(player, "§dMage Assistant", "Please give me the key.");
			goto("wait");
		}
		player.removeItem(player, key);
		stage.increase(player);
		msg.prefix(player, "§dMage Assistant", "To unluck the skills of the mage you have to do some tasks.");
		scheduler.msgPrefix(30, player, "§dMage Assistant", "A spider has nested in my cellar. Please go downstairs and kill it.");
		entity.spawn(cave_spider_loc, "CAVE_SPIDER");
	}
}
goto("wait");

@stage14
if(event == "living_death") {
	entity_type = entity.getType(living_entity);
	if(entity_type == "cave_spider") {
		stage.increase(player);
		msg.prefix(player, "§dMage Assistant", "Take this bucket, fill it with water and then empty it into the cauldron.");
		player.safeGiveItem(player, item.create("minecraft:bucket", 1, null, null));
		block.setMaterial(cauldron_block, cauldron_mat);
	}
}
goto("wait");

@stage15
if(event == "bucket_use") {
	cancel = false;
	if(block.gotWater(block)) {
		stage.increase(player);
	}
}
goto("wait");

@stage16
if(event == "block_click" && block != null) {
	item = living.getHand(player);
	if(block.getType(block) == cauldron_mat && item.getType(item) == water_bucket_mat) {
		cancel = false;
		msg.prefix(player, "§dMage Assistant", "Collect some netherwarts and give me all the ones you collect.");
		stage.increase(player);
		player.removeItem(player, water_bucket);
	}
}
goto("wait");

@stage17
if(event == "block_click") {
	if(block == null) {
		goto("wait");
	}
	block_type = block.getType(block);
	if(block_type == nether_wart_mat) {
		cancel = false;
	}
	goto("wait");
}
if(event == "block_break") {
	if(block.getType(block) == nether_wart_mat && warts_harvested < 4) {
		cancel = true;
		amount = math.random(1, 3);
		item.drop(item.create("nether_wart", amount, null, null), block.getLocation(block));
		warts_harvested += amount;
		if(warts_harvested >= 4) {
			stage.increase(player);
		}
	}
}
goto("wait");

@stage18
if(event == "entity_click" && slot.isHand(hand) && entity.isHuman(entity)) {
	entity_name = human.getName(entity);
	if(entity_name == "Mage Assistant") {
		rest_amount = human.giveItem(entity_name, player, nether_wart_mat, warts_harvested);
		if(rest_amount == 0) {
			player.unlockedMage(player);
			msg.prefix(player, "§dMage Assistant", "Thank you. You now can go to the Mage and buy skills.");
			quest.finishReward(script, player, 16);
		} else {
			warts_harvested = rest_amount;
		}
	}
}
goto("wait");

function opendoor(block) {
	block.setOpen(block, true);
	list.add($doors_to_close, block);
	if(!set.contains($visited_doors, block)) {
		set.add($visited_doors, block);
	}
}

@closedoor
doorblock = list.getIndex(doors_to_close, 0);
list.removeIndex(doors_to_close, 0);
block.setOpen(doorblock, false);
goto("wait");