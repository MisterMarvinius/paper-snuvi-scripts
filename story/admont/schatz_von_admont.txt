event.load("quest_start");
event.load("quest_term");
event.load("block_click");
event.load("entity_click");

world = world.getOverworld();
treasure_chest_loc = loc.new(world, 96, 66, -54);
mob_loc_1 = loc.new(world, 99, 65, -38);
mob_loc_2 = loc.new(world, 100, 66, -49);

stage = 0;
all_stages = 2;
quest_name = "Treasure of Admont";

@wait
wait();
if(!player.isQuester(player, script)) {
	goto("wait");
}
if(event == "quest_term") {
	//Hier Code einfügen...
	wait(); //Script wird von außen terminiert
}
if(event == "quest_start") {
	quest.display(player, quest_name, stage, all_stages);
	msg.prefix(player, "§dMiner", "In Admont there is a hidden treasure. Nobody has ever seen it...");
	scheduler.msgPrefix(30, player, "§dMiner", "I have tried it to find it since years.");
	scheduler.msgPrefix(60, player, "§dMiner", "Alena told me she had found a strange hole hidden in the tavern behind a door.");
	scheduler.msgPrefix(90, player, "§dMiner", "Maybe you have more success than me.");
	entity.spawn(mob_loc_1, "ZOMBIE");
	entity.spawn(mob_loc_1, "SKELETON");
	entity.spawn(mob_loc_2, "ZOMBIE");
	entity.spawn(mob_loc_2, "SKELETON");
	goto("wait");
}
label = string.concat("stage", string.number(stage));
goto(label);

@stage0
if(event == "block_click") {
	block_loc = block.getLocation(block);
	if(block_loc == treasure_chest_loc) {
		stage.increase(player);
		msg.prefix(player, "§dMiner", "Well done! Now come back to me.");
		inv = block.getInventory(block);
		inv.addItem(inv, item.create("GOLD_INGOT", 1, "Schatz von Admont", null));
	}
}
goto("wait");

@stage1
if(event == "entity_click" && slot.isHand(hand) && isCitizen(entity)) {
	entity_name = entity.getName(entity);
	string_name = string.text(entity_name);
	if(string_name == "Miner") {
		msg.prefix(player, "§dMiner", "You have actually made it. Whoever finds it can keep it.");
		msg.string(player, "§dQuest finished. Reward: 4 snuvis!");
		money.addBoost(player, 4);
		quest.finish(script, player);
	}
}
goto("wait");