p = read.player("marvinius");

shooting_item = item.new(material.get("minecraft:stick"));
enchantment.add(enchantment.get("infinity"), shooting_item, 1);
item.setName(shooting_item, text.new("§bStick of §mTruth§r §bSnow"));

player.giveItem(p, shooting_item);

event.load("block_click");
event.load("projectile_hit");

red_wool_mat = material.get("minecraft:red_wool");

@loop
wait();
if(event == "block_click") {
	if(action == "RIGHT_CLICK_AIR" || action == "RIGHT_CLICK_BLOCK") {
		hand = living.getEquip(player, read.slot("HAND"));
		offhand = living.getEquip(player, read.slot("OFF_HAND"));
		if(hand == shooting_item || offhand == shooting_item) {
			living.shootProjectile(player, "org.bukkit.entity.Snowball");
		}
	}
	goto("loop");
}
if(event == "projectile_hit") {
	if(shooter == p) {
		if(block_hit != null) {
			colorCentre(block.getLocation(block_hit));
		}
	}
	goto("loop");
}
goto("loop");

function colorCentre(location) {
	block = block.get(location);
	block.setMaterial(block, $red_wool_mat);
	
	colorNeighbour(loc.mod(location, 0, 1, 0));
	colorNeighbour(loc.mod(location, 0, -1, 0));
	colorNeighbour(loc.mod(location, 0, 0, 1));
	colorNeighbour(loc.mod(location, 0, 0, -1));
	colorNeighbour(loc.mod(location, 1, 0, 0));
	colorNeighbour(loc.mod(location, -1, 0, 0));
}

function colorNeighbour(location) {
	block = block.get(location);
	if(!block.isAir(block)) {
		block.setMaterial(block, $red_wool_mat);
	}
}

function block.isAir(block) {
	type = block.getType(block);
	return type == material.get("AIR") || type == material.get("CAVE_AIR");
}