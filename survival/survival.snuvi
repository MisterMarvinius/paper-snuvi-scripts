import "utils/block";
import "utils/clan";
import "utils/display";
import "utils/msg";
import "utils/time";

event.load("entity_click");
event.load("snuvi_click");
event.load("player_pre_respawn");
event.load("player_post_respawn");
event.load("living_death");
//event.load("player_join");
event.load("player_move");
event.load("block_click");
event.load("entity_damage");
event.load("entity_change_block");

clan.loadData();

afk_loop_map = map.new();
survival_respawn = set.new();

overworld = world.getOverWorld();
moveid_gamesspawn = event.addMoveData(loc.new(overworld, 93, 61, -31), loc.new(overworld, 97, 65, -30), 5, -1);
moveid_creaspawn = event.addMoveData(loc.new(overworld, 102, 60, -20), loc.new(overworld, 103, 64, -16), 5, -1);
moveid_mobarena = event.addMoveData(loc.new(overworld, 81, 59, -28), loc.new(overworld, 82, 63, -24), 5, -1);
moveid_afk_down = event.addMoveData(loc.new(overworld, 135, 66, -59), loc.new(overworld, 136, 67, -58), 20, -1);
moveid_afk_up = event.addMoveData(loc.new(overworld, 170, 34, -47), loc.new(overworld, 171, 35, -46), 20, -1);
moveid_tpcave_down = event.addMoveData(loc.new(overworld, 103, 80, -12), loc.new(overworld, 104, 81, -11), 20, -1);
moveid_tpcave_up = event.addMoveData(loc.new(overworld, 91, 60, -22), loc.new(overworld, 92, 61, -21), 20, -1);

daily_reward_loc = loc.new(overworld, 136, 77, -22);

itemhunter_ass_loc = loc.new(overworld, 137, 78.5, -19);
itemhunter_frame_loc = loc.new(overworld, 135, 76, -19);
itemhunter_reward = 7; // snuvis
itemhunter_frame_direction = "WEST";
itemhunter_default_seconds = 300;
itemhunter_seconds = itemhunter_default_seconds;
itemhunter_items = list.new();
itemhunter.addItem("minecraft:rail");
itemhunter.addItem("minecraft:oak_fence");
itemhunter.addItem("minecraft:glass_bottle");
itemhunter.addItem("minecraft:dropper");
itemhunter.addItem("minecraft:iron_sword");
itemhunter.addItem("minecraft:ladder");
itemhunter.addItem("minecraft:minecart");
itemhunter.addItem("minecraft:hopper");
itemhunter.addItem("minecraft:redstone_torch");
itemhunter.addItem("minecraft:torch");
itemhunter.addItem("minecraft:bow");
itemhunter.addItem("minecraft:cobblestone_wall");
itemhunter.addItem("minecraft:stone");
itemhunter.addItem("minecraft:oak_door");
itemhunter.addItem("minecraft:oak_fence_gate");
itemhunter.addItem("minecraft:stone_pressure_plate");
itemhunter.addItem("minecraft:oak_pressure_plate");
itemhunter.addItem("minecraft:oak_trapdoor");
itemhunter.addItem("minecraft:bucket");
itemhunter.addItem("minecraft:iron_bars");
itemhunter.addItem("minecraft:oak_sign");
itemhunter.addItem("minecraft:shears");
itemhunter.addItem("minecraft:fishing_rod");
itemhunter.addItem("minecraft:compass");
itemhunter.addItem("minecraft:oak_boat");
itemhunter.addItem("minecraft:bowl");
itemhunter.addItem("minecraft:white_wool");
itemhunter.addItem("minecraft:wooden_sword");
itemhunter.addItem("minecraft:tripwire_hook");
itemhunter.addItem("minecraft:chest_minecart");
itemhunter.addItem("minecraft:iron_door");
itemhunter.addItem("minecraft:smooth_stone_slab");
itemhunter.addItem("minecraft:activator_rail");
itemhunter.addItem("minecraft:detector_rail");
itemhunter.addItem("minecraft:dispenser");
itemhunter.addItem("minecraft:piston");
itemhunter.addItem("minecraft:repeater");
itemhunter.addItem("minecraft:comparator");
itemhunter.addItem("minecraft:heavy_weighted_pressure_plate");
itemhunter.addItem("minecraft:arrow");
itemhunter.addItem("minecraft:bookshelf");
itemhunter.addItem("minecraft:mushroom_stew");
itemhunter.addItem("minecraft:pumpkin_pie");
itemhunter.addItem("minecraft:cooked_beef");
itemhunter.addItem("minecraft:cooked_porkchop");
itemhunter.addItem("minecraft:book");
itemhunter.addItem("minecraft:map");
itemhunter.addItem("minecraft:ender_eye");
itemhunter.addItem("minecraft:lead");
itemhunter.addItem("minecraft:stone_brick_slab");
itemhunter.addItem("minecraft:brewing_stand");
itemhunter.addItem("minecraft:leather_helmet");
itemhunter.addItem("minecraft:leather_chestplate");
itemhunter.addItem("minecraft:leather_leggings");
itemhunter.addItem("minecraft:leather_boots");
itemhunter.addItem("minecraft:cauldron");
itemhunter.addItem("minecraft:clock");
itemhunter.addItem("minecraft:paper");
itemhunter.addItem("minecraft:tnt");
itemhunter.addItem("minecraft:magma_cream");
itemhunter.addItem("minecraft:bread");
itemhunter.addItem("minecraft:white_bed");
itemhunter.addItem("minecraft:cake");
itemhunter.addItem("minecraft:cooked_cod");
itemhunter.addItem("minecraft:tnt_minecart");
itemhunter.addItem("minecraft:melon");
itemhunter.addItem("minecraft:hay_block");
itemhunter.addItem("minecraft:ender_chest");
itemhunter.addItem("minecraft:sticky_piston");
itemhunter.addItem("minecraft:daylight_detector");
itemhunter.addItem("minecraft:golden_apple");
itemhunter.addItem("minecraft:jukebox");
itemhunter.addItem("minecraft:anvil");
itemhunter.addItem("minecraft:quartz_stairs");
itemhunter.addItem("minecraft:enchanting_table");
itemhunter.addItem("minecraft:golden_carrot");
itemhunter.addItem("minecraft:glistering_melon_slice");
itemhunter.addItem("minecraft:redstone_lamp");
itemhunter.addItem("minecraft:writable_book");
itemhunter.addItem("minecraft:powered_rail");
itemhunter.addItem("minecraft:stick");
itemhunter.addItem("minecraft:wooden_pickaxe");
itemhunter.addItem("minecraft:iron_pickaxe");
itemhunter.addItem("minecraft:golden_pickaxe");
itemhunter.addItem("minecraft:wooden_hoe");
itemhunter.addItem("minecraft:diamond_hoe");
itemhunter.addItem("minecraft:golden_hoe");
itemhunter.addItem("minecraft:diamond_chestplate");
itemhunter.addItem("minecraft:iron_chestplate");
itemhunter.addItem("minecraft:golden_boots");
itemhunter.addItem("minecraft:diamond_boots");
itemhunter.addItem("minecraft:iron_helmet");
itemhunter.addItem("minecraft:iron_leggings");
itemhunter.addItem("minecraft:iron_boots");
itemhunter.addItem("minecraft:golden_leggings");
itemhunter.addItem("minecraft:golden_helmet");
itemhunter.addItem("minecraft:golden_chestplate");

config = getScriptVar("server_config");
mobarena_spawn = loc.new(overworld, 1150.5, -5, 1087, 90, 0);
afk_down_loc = loc.new(overworld, 168.5, 34, -46.5, 135, 25);
afk_up_loc = loc.new(overworld, 137.5, 66.6, -56.5, -35, 0);
tpcave_down_loc = loc.new(overworld, 95.5, 60, -20.5, -180, 0);
tpcave_up_loc = loc.new(overworld, 104.5, 80, -9.5, -145, 0);
prefix_skill = "§2Skill";
prefix_money = "§2Money";

redstone_mat = material.get("redstone");
ice_mat = material.get("ice");
blue_ice_mat = material.get("blue_ice");
packed_ice_mat = material.get("packed_ice");

ench_flame = enchantment.get("flame");
ench_unbreaking = enchantment.get("unbreaking");
ench_sharpness = enchantment.get("sharpness");
piston_sound = sound.get("block.piston.extend");
adv_tp_sound = sound.get("block.portal.travel");
sound_category_ambient = sound.getCategory("AMBIENT");

sound_category_master = sound.getCategory("MASTER");
sound_flute = sound.get("block.note_block.flute");
sound_harp = sound.get("block.note_block.harp");
sound_pling = sound.get("block.note_block.pling");
sound_cat = sound.get("entity.cat.ambient");
sound_stray_cat = sound.get("entity.cat.stray_ambient");
sound_purreow = sound.get("entity.cat.purreow");
sound_drink_milk = sound.get("entity.wandering_trader.drink_milk");
sound_slime_place = sound.get("block.slime_block.place");
sound_eating = sound.get("entity.generic.eat");

music_chirp = sound.get("music_disc.chirp");

Sound_Pitch_List = list.new();
for(a = -12; a <= 12; a++){
	list.add(Sound_Pitch_List, math.pow(2, -a / 12));
}

casino_inv = inv.new("022200200", text.new("Casino"));
casino_inv_id = inv.getId(casino_inv);
lore_list = list.new();
list.add(lore_list, text.new("§dClick to lose or double"));
list.add(lore_list, text.new("§dWinrate: 45%"));
inv.setItem(casino_inv, 1, customitem.create("COPPER_COIN", 1, "§fGamble 1 Snuvi", lore_list));
inv.setItem(casino_inv, 2, customitem.create("SILVER_COIN", 1, "§fGamble 64 Snuvis", lore_list));
inv.setItem(casino_inv, 3, customitem.create("GOLD_COIN", 1, "§fGamble 4096 Snuvis", lore_list));;
lore_list = list.new();
lottery_value = lottery.getValue();
list.add(lore_list, text.new(string.concat("§dWin value: ", string.number(lottery_value))));
list.add(lore_list, text.new("§dBuy a ticket for 10 snuvis."));
list.add(lore_list, text.new("§dWinrate 1:500"));
inv.setItem(casino_inv, 6, customitem.create("COPPER_COIN", 1, "§fLottery", lore_list));;

bankmenu = inv.new("222200000", text.new("Banker"));
bankinvid = inv.getId(bankmenu);
lore_list = list.new();
list.add(lore_list, text.new("§aLeftclick §f= §eWithdraw"));
list.add(lore_list, text.new("§cRightclick §f= §eDeposit"));
inv.setItem(bankmenu, 0, customitem.create("COPPER_COIN", 1, "§f1 Snuvi", lore_list));
inv.setItem(bankmenu, 1, customitem.create("SILVER_COIN", 1, "§f64 Snuvis", lore_list));
inv.setItem(bankmenu, 2, customitem.create("GOLD_COIN", 1, "§f4096 Snuvis", lore_list));
inv.setItem(bankmenu, 3, item.create("NETHERITE_BLOCK", 1, "§fAll Snuvis", lore_list));

itemhunter.setNextItem();
sgoto(20, "itemhunter_loop");

msg.string("dev", "§bSurvival §rloaded.");
@wait
wait();
if(event == "player_move") {
	if(id == moveid_tpcave_up) {
		player.setBackPosLoc(player, tpcave_down_loc);
		player.teleport(player, tpcave_up_loc, false);
		goto("wait");
	}
	if(id == moveid_tpcave_down) {
		player.setBackPosLoc(player, tpcave_up_loc);
		player.teleport(player, tpcave_down_loc, false);
		goto("wait");
	}
	if(id == moveid_afk_up) {
		player.setBackPosLoc(player, afk_down_loc);
		player.teleport(player, afk_up_loc, false);
		goto("wait");
	}
	if(id == moveid_afk_down) {
		player.setBackPosLoc(player, afk_up_loc);
		player.teleport(player, afk_down_loc, false);
		goto("wait");
	}
	if(id == moveid_mobarena) {
		player.setBackPosLoc(player, world.getServerSpawn());
		player.teleport(player, mobarena_spawn, false);
		goto("wait");
	}
	if(id == moveid_gamesspawn) {
		player.setBackPosLoc(player, world.getServerSpawn());
		player.teleport(player, world.getGamesSpawn(), false);
		goto("wait");
	}
	if(id == moveid_creaspawn) {
		player.setBackPosLoc(player, world.getServerSpawn());
		player.teleport(player, world.getCreativeSpawn(), false);
		goto("wait");
	}
	goto("wait");
}
//Wenn Event aus Survival-Welten, dann wird er verarbeitet
if(event == "living_death") {
	loc = entity.getLocation(living_entity);
} elseif(event == "entity_damage" || event == "entity_change_block") {
	loc = entity.getLocation(entity);
} else {
	loc = entity.getLocation(player);
}
world_name = world.getName(loc.getWorld(loc));
if(world.isSurvName(world_name)) {
	ignoreGoto(event);
}
goto("wait");

@living_death
if(isPlayer(living_entity)) {
	player.setDeathLoc(living_entity);
}
goto("wait");

@entity_change_block
if(entity.getType(entity) == "enderman") {
	cancel = true;
}
goto("wait");

@entity_damage
if(!isPlayer(entity)) {
	damager = player.getFromDamageSource(damage_source);
	if(!isPlayer(damager)) {
		goto("wait");
	}
	pet_type = entity.getType(entity);
	if(pet_type == "wolf" || pet_type == "cat" || pet_type == "parrot" || pet_type == "horse") {
		if(pet.isTamed(entity)) {
			owner = pet.getOwner(entity);
			if(owner == damager) {
				if(player.getNoPetDamage(damager)) {
					cancel = true;
				}
			}
		}
	}
	goto("wait");
}
player = entity;
damager = player.getFromDamageSource(damage_source);
if(damager == null) {
	goto("wait");
}
if(player == damager) {
	goto("wait");
}
if(!hasPvpOn(damager)) {
	cancel = true;
	msg.prefix(damager, "§5PvP", "You have PvP disabled.");
	goto("wait");
}
if(!hasPvpOn(player)) {
	cancel = true;
	msg.prefix(damager, "§5PvP", string.concat(player.getName(player), " has PvP disabled."));
	goto("wait");
}
goto("wait");

@entity_click
if(slot.isOffHand(hand)) {
	goto("wait");
}
if(!isCitizen(entity)) {
	goto("wait");
}
entity_name = entity.getName(entity);
string_name = string.text(entity_name);
if(string_name == "Item Hunter") {
	itemhunter.shopItem(player);
	goto("wait");
}
if(string_name == "Casino") {
	inv.open(casino_inv, player);
	goto("wait");
}
if(string_name == "Banker") {
	inv.open(bankmenu, player);
	goto("wait");
}
if(string_name == "Marvin") {
	item = living.getHand(player);
	item_name = item.getName(item);
	if(item_name != null && string.text(item_name) == "§fBeer") {
		sound.spawnForPlayer(player, $sound_drink_milk, $sound_category_master, 1, 1);
		msg.prefix(player, "§cmarvinius", "Prost!");
	} else {
		msg.prefix(player, "§cmarvinius", "Ich mag Gösser.");
	}
	goto("wait");
}
if(string_name == "Kajetan") {
	item = living.getHand(player);
	item_type = item.getType(item);
	sound.spawnForPlayer(player, $sound_slime_place, $sound_category_master, 1, 1);
	if(item_type == redstone_mat) {
		rand_number = math.random(0, 3);
		if(rand_number == 0){
			temp_message = "WUSIWUSIWUSIWUSIWUSIWUSIWUSIWUSI";
		}
		if(rand_number == 1){
			temp_message = "Wusi!";
		}
		if(rand_number == 2){
			temp_message = "Wusi?";
		}
		if(rand_number == 3){
			temp_message = "Wusi.";
		}
		msg.prefix(player, "§4kajetanjohannes", temp_message);
		goto("wait");
	}
	if(item_type == ice_mat || item_type == blue_ice_mat || item_type == packed_ice_mat){
		msg.prefix(player, "§4kajetanjohannes", "§bDas sieht nach mir aus.");
	}else{
		msg.prefix(player, "§4kajetanjohannes", "wusi");
	}
	goto("wait");
}
if(string_name == "Lucas") {
	item = living.getHand(player);
	item_name = item.getName(item);
	if(item_name != null && string.text(item_name) == "§fBeer") {
		msg.prefix(player, "§cMentorMentos", "Folg mir auf Twitch! ");
		msg(player, string.getLinkText("Klick mich an :D","https://www.twitch.tv/Mentor_Mentos"));
	} else {
		msg.prefix(player, "§cMentorMentos", "Ich mag Kölsch.");
	}
	goto("wait");
}
if(string_name == "Jan") {
	if(player.isSneaking(player)){
		msg.prefix(player, "§9SirTerence7", "Very secretive yes yes.");
		goto("wait");
	}
	rand_number = math.random(0, 7);
	if(rand_number < 4){
		msg.prefix(player, "§9SirTerence7", "Mathe bla... bla... bla...");
	}
	if(rand_number == 4){
		msg.prefix(player, "§9SirTerence7", "Fraktale bla... bla... bla...");
	}
	if(rand_number == 5){
		msg.prefix(player, "§9SirTerence7", "Höhere Dimensionen bla... bla... bla...");
	}
	if(rand_number == 6){
		msg.prefix(player, "§9SirTerence7", "*redet mit Händen weiter*");
	}
	if(rand_number == 7){
		msg.prefix(player, "§9SirTerence7", "*wechselt voller Faszination zwischen scheinbar zusammenhangslosen Themen hin und her*");
	}
	rand_number = math.random(0, 500);
	if(rand_number <= 2){
		sound.spawnForPlayer(player, $music_chirp, $sound_category_master, 1, 1);
	} else {
		rand_pitch = math.random(4, list.getSize(Sound_Pitch_List) - 5);
		if(rand_number < 200){
			pitch1 = list.getIndex(Sound_Pitch_List, rand_pitch-4);
			pitch2 = list.getIndex(Sound_Pitch_List, rand_pitch);
			pitch3 = list.getIndex(Sound_Pitch_List, rand_pitch+3);
		} else {
			if(rand_number < 300){
				pitch1 = list.getIndex(Sound_Pitch_List, rand_pitch-4);
				pitch1_2 = list.getIndex(Sound_Pitch_List, rand_pitch-2);
				pitch2 = list.getIndex(Sound_Pitch_List, rand_pitch);
				pitch3_2 = list.getIndex(Sound_Pitch_List, rand_pitch+1);
				pitch3 = list.getIndex(Sound_Pitch_List, rand_pitch+3);
				sound.spawnForPlayer(player, $sound_harp, $sound_category_master, 0.5, pitch1_2);
				sound.spawnForPlayer(player, $sound_harp, $sound_category_master, 0.5, pitch3_2);
				sound.spawnForPlayer(player, $sound_flute, $sound_category_master, 0.75, pitch1_2);
				sound.spawnForPlayer(player, $sound_flute, $sound_category_master, 0.75, pitch3_2);
			} else {
				pitch1 = list.getIndex(Sound_Pitch_List, rand_pitch-3);
				pitch2 = list.getIndex(Sound_Pitch_List, rand_pitch);
				pitch3 = list.getIndex(Sound_Pitch_List, rand_pitch+4);
			}
		}
		sound.spawnForPlayer(player, $sound_harp, $sound_category_master, 0.8, pitch1);
		sound.spawnForPlayer(player, $sound_harp, $sound_category_master, 0.9, pitch2);
		sound.spawnForPlayer(player, $sound_harp, $sound_category_master, 0.8, pitch2);
		sound.spawnForPlayer(player, $sound_flute, $sound_category_master, 0.9, pitch1);
		sound.spawnForPlayer(player, $sound_flute, $sound_category_master, 1, pitch2);
		sound.spawnForPlayer(player, $sound_flute, $sound_category_master, 0.9, pitch3);
	}
	goto("wait");
}
if(string_name == "Mareen") {
	rand_number = math.random(0, 5);
	if(rand_number > 1){
		msg.prefix(player, "§9Mareeeen", "Wo ist die Schokolade versteckt!?");
	}else{
		msg.prefix(player, "§9Mareeeen", "Ich esse deine Schokolade!");
		sound.spawnForPlayer(player, $sound_eating, $sound_category_master, 1, 1);
	}
	goto("wait");
}

// info point
if(string_name == "Olaf") {
	msg.string(player, string.getSpacer());
	msg(player, string.getClickText("§r [§cclick§r] [§eSurvival§r]", "/infopoint1"));
	//msg(player, string.getClickText("§r [§cclick§r] [§eAdventure§r]", "/infopoint2"));
	msg(player, string.getClickText("§r [§cclick§r] [§ePlots§r]", "/infopoint3"));
	//msg(player, string.getClickText("§r [§cclick§r] [§eClans§r]", "/infopoint4"));
	msg(player, string.getClickText("§r [§cclick§r] [§eCities§r]", "/infopoint5"));
	msg(player, string.getClickText("§r [§cclick§r] [§eShops§r]", "/infopoint6"));
	msg(player, text.merge(string.getClickText("§r [§cclick§r] [§eMinigames§r]", "/infopoint7"), text.new(" "), string.getClickText("§f[§bTeleport§f]", "/games")));
	msg(player, string.getClickText("§r [§cclick§r] [§eQuests§r]", "/infopoint8"));
	msg(player, text.merge(string.getClickText("§r [§cclick§r] [§eCreative§r]", "/infopoint12"), text.new(" "), string.getClickText("§f[§bTeleport§f]", "/creative")));
	msg(player, text.merge(string.getClickText("§r [§cclick§r] [§eRanking§r]", "/infopoint9"), text.new(" "), string.getLinkText("§f[§bRanks§f]", "https://mundus-crassus.de/?site=ranks")));
	msg(player, text.merge(string.getClickText("§r [§cclick§r] [§eJokes§r]", "/infopoint10"), text.new(" "), string.getLinkText("§f[§bJokes§f]", "https://mundus-crassus.de/?site=rules")));
	goto("wait");
}
goto("wait");
if(string_name == "Adventure") {
	if(!player.isOnAdventure(player)) {
		msg.prefix(player, "§5Adventure", "You aren't on an adventure. Click the chest in the teleporter to start an adventure.");
		goto("wait");
	}
	player.setAdventure(player, false);
	adventures = player.getAdventureAmounts(player);
	if(adventures == null) {
		adventures = 0;
	}
	player.setAdventureAmounts(player, ++adventures);
	msg.prefix(player, "§5Adventure", "Congratulations, you finished your adventure, here ist your reward!");
	if(adventures == 10) {
		offerRank(player, "rank.pioneer");
	} elseif(adventures == 5) {
		offerRank(player, "rank.explorer");
	} elseif(adventures == 1) {
		offerRank(player, "rank.adventurer");
	}
	//removeAdventureDisplay(player);
	player.safeGiveItem(player, customitem.getAmber());
	money.addBoost(player, 4096);
	goto("wait");
}
goto("wait");

@player_join
if(player.isOnAdventure(player)) {
	//addAdventureDisplay(player, adventure_aim_loc);
}
goto("wait");

@snuvi_click
title_string = string.text(inv_title);
if(title_string == "Daily Reward") {
	item = inv.getItem(inv, inv_slot);
	lore_list = item.getLore(item);
	lore_string_0 = string.removeFormat(string.text(list.getIndex(lore_list, 0)));
	if(lore_string_0 == "Heute abholen!") {
		player.setLastVoteTime(player, time.getMillis());
		dr_value = player.getDailyReward(player);
		dr_value <<= 1; // shift all days to the left
		dr_value = bit.set(dr_value, 0); // set today's bit
		player.setDailyReward(player, dr_value);
		player.setDailyRewardTime(player, time.getMillis());
		list.setIndex(lore_list, 0, text.new("Bereits abgeholt"));
		item.setLore(item, lore_list);
		if(inv_slot == 0) {
			reward_text = "1 Snuvi";
			money.add(player, 1);
		} elseif(inv_slot == 1) {
			reward_text = "2 Snuvi";
			money.add(player, 2);
		} elseif(inv_slot == 2) {
			reward_text = "4 Snuvi";
			money.add(player, 4);
		} elseif(inv_slot == 3) {
			reward_text = "8 Snuvi";
			money.add(player, 8);
		} elseif(inv_slot == 4) {
			reward_text = "16 Snuvi";
			money.add(player, 16);
		} elseif(inv_slot == 5) {
			reward_text = "32 Snuvi";
			money.add(player, 32);
		} elseif(inv_slot == 6) {
			reward_text = "1 Amber";
			reward_item = customitem.create("AMBER", 1, null, null);
			player.safeGiveItem(player, reward_item);
		}
		msg.prefix(player, "§6Daily Reward", string.concat("Daily reward received: ", reward_text));
	}
	goto("wait");
}

inv_id = inv.getId(inv);
if(inv_id == bankinvid) { //Bankmenü
	//Einzahlen
	if(click == "RIGHT" || click == "SHIFT_RIGHT") {
		amount_items = 1;
		if(click == "SHIFT_RIGHT") {
			amount_items = 64;
		}
		player_inv = player.getInv(player);
		gold_item = customitem.create("GOLD_COIN", 1, null, null);
		silver_item = customitem.create("SILVER_COIN", 1, null, null);
		copper_item = customitem.create("COPPER_COIN", 1, null, null);
		gold_inv = inv.getItemAmount(player_inv, gold_item);
		silver_inv = inv.getItemAmount(player_inv, silver_item);
		copper_inv = inv.getItemAmount(player_inv, copper_item);
		if(inv_slot == 0) {
			depositSnuvis(player, amount_items, 0, 0);
		}
		elseif(inv_slot == 1) {
			depositSnuvis(player, 0, amount_items, 0);
		}
		elseif(inv_slot == 2) {
			depositSnuvis(player, 0, 0, amount_items);
		}
		elseif(inv_slot == 3) {
			gold_inv = inv.getItemAmount(player_inv, customitem.create("GOLD_COIN", 1, null, null));
			silver_inv = inv.getItemAmount(player_inv, customitem.create("SILVER_COIN", 1, null, null));
			copper_inv = inv.getItemAmount(player_inv, customitem.create("COPPER_COIN", 1, null, null));
			depositSnuvis(player, copper_inv, silver_inv, gold_inv);
		}
		goto("wait");
	}
	//Auszahlen
	if(click == "LEFT" || click == "SHIFT_LEFT") {
		amount_items = 1;
		if(click == "SHIFT_LEFT") {
			amount_items = 64;
		}
		if(inv_slot == 0) {
			withdrawSnuvis(player, amount_items, 0, 0);
		}
		elseif(inv_slot == 1) {
			withdrawSnuvis(player, 0, amount_items, 0);
		}
		elseif(inv_slot == 2) {
			withdrawSnuvis(player, 0, 0, amount_items);
		}
		elseif(inv_slot == 3) {
			a = money.split(money.get(player));
			withdrawSnuvis(player, a[2], a[1], a[0]);
		}
	}
	goto("wait");
}
if(inv_id == casino_inv_id) {
	//Lottery
	if(inv_slot == 6) {
		ticket_price = 10;
		if(!hasEnoughMoney(player, ticket_price)) {
			msg.action(player, "Not enough snuvis on your account.");
			goto("wait");
		}
		money.sub(player, ticket_price);
		new_value = lottery.addValue(10);
		item = inv.getItem(inv, inv_slot);
		lore_list = item.getLore(item);
		list.setIndex(lore_list, 0, text.new(string.concat("§dWin value: ", string.number(new_value))));
		item.setLore(item, lore_list);
		win = math.random(1, 500);
		if(win == 1) {
			message = string.concat(player.getName(player), " won ", string.number(new_value), " snuvis.");
			msg.online("§dLottery", message);
			money.add(player, new_value);
			list.setIndex(lore_list, 0, text.new(string.concat("§dWin value: 0")));
			item.setLore(item, lore_list);
			lottery.setValue(0);
		}
		goto("wait");
	}
	//Gambler
	if(inv_slot == 1) {
		amount = 1;
	} elseif(inv_slot == 2) {
		amount = 64;
	} elseif(inv_slot == 3) {
		amount = 4096;
	}
	if(!hasEnoughMoney(player, amount)) {
		msg.action(player, "Not enough snuvis on your account.");
		goto("wait");
	}
	win = math.random(1, 100);
	if(win < 45) {
		money.add(player, amount);
		temp = " won ";
	} else {
		money.sub(player, amount);
		temp = " lost ";
	}
	if(amount == 4096) {
		message = string.concat(player.getName(player), temp, string.number(amount), " snuvis.");
		msg.radius("§dGambler", message, entity.getLocation(player), 10);
	}
}
goto("wait");

function depositSnuvis(player, copper, silver, gold) {
	gold_item = customitem.create("GOLD_COIN", 1, null, null);
	silver_item = customitem.create("SILVER_COIN", 1, null, null);
	copper_item = customitem.create("COPPER_COIN", 1, null, null);
	player_inv = player.getInv(player);
	gold_inv = inv.getItemAmount(player_inv, gold_item);
	silver_inv = inv.getItemAmount(player_inv, silver_item);
	copper_inv = inv.getItemAmount(player_inv, copper_item);
	if(copper > copper_inv || silver > silver_inv || gold > gold_inv) {
		player.action(player, text.new("Not enough coins in inventory."));
		return;
	}
	if(copper > 0) {
		item.setAmount(copper_item, copper);
		inv.removeItem(player_inv, copper_item);
	}
	if(silver > 0) {
		item.setAmount(silver_item, silver);
		inv.removeItem(player_inv, silver_item);
	}
	if(gold > 0) {
		item.setAmount(gold_item, gold);
		inv.removeItem(player_inv, gold_item);
	}
	sum = copper + silver * 64 + gold * 4096;
	money.add(player, sum);
}

function withdrawSnuvis(player, copper, silver, gold) {
	sum = copper + silver * 64 + gold * 4096;
	if(!hasEnoughMoney(player, sum)) {
		player.action(player, text.new("Not enough snuvis on your account."));
		return;
	}
	if(copper > 0) {
		copper_item = customitem.create("COPPER_COIN", 1, null, null);
		item.setAmount(copper_item, copper);
		player.safeGiveItem(player, copper_item);
	}
	if(silver > 0) {
		silver_item = customitem.create("SILVER_COIN", 1, null, null);
		item.setAmount(silver_item, silver);
		player.safeGiveItem(player, silver_item);
	}
	if(gold > 0) {
		gold_item = customitem.create("GOLD_COIN", 1, null, null);
		item.setAmount(gold_item, gold);
		player.safeGiveItem(player, gold_item);
	}
	sum = copper + silver * 64 + gold * 4096;
	money.sub(player, sum);
}

@block_click
if(block != null) {
	block_loc = block.getLocation(block);
	if(block_loc == daily_reward_loc) {
		cancel = true;
		dr_value = player.getDailyReward(player);
		dr_time = player.getDailyRewardTime(player);
		now_time = time.getMillis();
		// reset if all rewards claimed and it is the follow up day
		if(dr_value >= 127 && isFollowingDay(dr_time, now_time)) {
			player.setDailyReward(player, 0);
			player.setDailyRewardTime(player, 0);
			dr_value = 0;
			dr_time = 0;
		}
		// reset if at least one day has been skipped
		if(time.hasWholeDaySkipped(dr_time, now_time)) {
			player.setDailyReward(player, 0);
			player.setDailyRewardTime(player, 0);
			dr_value = 0;
			dr_time = 0;
		}
		
		daily_reward_inv = inv.new("222222200", text.new("Daily Reward"));
		canGetRewardTodayAlreadyShown = false;
		for(i = 0; i < 7; i++) {
			dr_bit = bit.get(dr_value, i);
			if(dr_bit) {
				lore = "Bereits abgeholt";
			} elseif((dr_time == 0 || isFollowingDay(dr_time, now_time)) && !canGetRewardTodayAlreadyShown) {
				lore = "Heute abholen!";
				canGetRewardTodayAlreadyShown = true;
			} else {
				lore = "An einem anderen Tag abzuholen";
			}
			if(i == 0) {
				inv.setItem(daily_reward_inv, 0, customitem.create("COPPER_COIN", 1, "§f1 Snuvi", lore));
			} elseif(i == 1) {
				inv.setItem(daily_reward_inv, 1, customitem.create("COPPER_COIN", 2, "§f2 Snuvi", lore));
			} elseif(i == 2) {
				inv.setItem(daily_reward_inv, 2, customitem.create("COPPER_COIN", 4, "§f4 Snuvi", lore));
			} elseif(i == 3) {
				inv.setItem(daily_reward_inv, 3, customitem.create("COPPER_COIN", 8, "§f8 Snuvi", lore));
			} elseif(i == 4) {
				inv.setItem(daily_reward_inv, 4, customitem.create("COPPER_COIN", 16, "§f16 Snuvi", lore));
			} elseif(i == 5) {
				inv.setItem(daily_reward_inv, 5, customitem.create("COPPER_COIN", 32, "§f32 Snuvi", lore));
			} elseif(i == 6) {
				inv.setItem(daily_reward_inv, 6, customitem.create("AMBER", 1, "§f1 Amber", lore));
			}
		}
		
		
		inv.open(daily_reward_inv, player);
		goto("wait");
	}
}
if(action == "RIGHT_CLICK_AIR") {
	player_loc = entity.getLocation(player);
	world = loc.getWorld(player_loc);
	if(!world.isOverworld(world.getName(world))) {
		goto("wait");
	}
	item = living.getHand(player);
	if(!item.isType(item, "CLOCK")) {
		goto("wait");
	}
	time = world.getTime(world);
	time_h = math.roundDown(time/1000) + 6;
	while(time_h >= 24) {
		time_h -= 24;
	}
	//m min = t ticks * 60m / 1000 ticks
	time_m = time%1000;
	time_m = math.roundDown(time_m * 60 / 1000);
	if(time_m < 10) {
		time_m = string.concat("0", string.number(time_m));
	} else {
		time_m = string.number(time_m);
	}
	msg.prefix(player, "§eClock", string.concat(string.number(time_h), ":", time_m));
	goto("wait");
}
if(action == "RIGHT_CLICK_BLOCK") {
	if(block.isType(block, "SPAWNER")) {
		if(!player.isCreative(player)) {
			cancel = true;
			goto("wait");
		}
	}
}
goto("wait");

@player_pre_respawn
if(loc.isInSurvWorld(entity.getLocation(player))) {
	set.add(survival_respawn, player.getUuid(player));
}
goto("wait");

@player_post_respawn
player_uuid = player.getUuid(player);
if(set.contains(survival_respawn, player_uuid)) {
	set.remove(survival_respawn, player_uuid);
	if(player.isOnAdventure(player)) {
		tp_loc = player.getAdventureStart(player);
		entity.teleport(player, tp_loc);
		goto("wait");
	}
	entity.teleport(player, world.getServerSpawn());
}
goto("wait");

@itemhunter_loop
itemhunter.decreaseSecondsAndProcess();
sgoto(20, "itemhunter_loop");
goto("wait");

function lottery.addValue(value) {
	config = getServerConfig();
	new_value = config.getDouble(config, "lottery_value", 0) + value;
	config.set(config, "lottery_value", new_value);
	config.saveAsync(config);
	return new_value;
}

function lottery.setValue(value) {
	config = getServerConfig();
	config.set(config, "lottery_value", value);
	config.saveAsync(config);
}

function lottery.getValue() {
	config = getServerConfig();
	return config.getDouble(config, "lottery_value", 0);
}

function player.getDailyReward(player_or_id) {
	config = playerdata.getSurvival(player_or_id);
	return config.getDouble(config, "dailyreward", 0);
}

function player.setDailyReward(player_or_id, value) {
	config = playerdata.getSurvival(player_or_id);
	config.set(config, "dailyreward", value);
	config.saveAsync(config);
}

function player.setDailyRewardTime(player_or_id, millis) {
	config = playerdata.getSurvival(player_or_id);
	config.set(config, "lastdailyrewardtime", millis);
	config.saveAsync(config);
}

function player.getDailyRewardTime(player_or_id) {
	config = playerdata.getSurvival(player_or_id);
	return config.getDouble(config, "lastdailyrewardtime", 0);
}

function itemhunter.addItem(material) {
	list.add($itemhunter_items, item.new(material.get(material)));
}

function itemhunter.decreaseSecondsAndProcess() {
	$itemhunter_seconds--;
	if($itemhunter_seconds <= 0) {
		itemhunter.setNextItem();
		return;
	}
	itemhunter.updateName();
}

function itemhunter.updateName() {
    itemhunter_disp = getDisplayAtLoc($itemhunter_ass_loc, 1);
    if(itemhunter_disp == null) {
        return;
    }
    t = string.replace(t, "&", "§");
    display.setText(itemhunter_disp, text.new(string.number($itemhunter_seconds)));
}

function itemhunter.resetTimer() {
	$itemhunter_seconds = $itemhunter_default_seconds;
}

function itemhunter.setItemFrame(item) {
	entity.removeAll("org.bukkit.entity.ItemFrame", $itemhunter_frame_loc, 1);
	entity.frame.spawn($itemhunter_frame_loc, $itemhunter_frame_direction, item);
}

function itemhunter.getRandomItem() {
	random_index = math.random(0, list.getSize($itemhunter_items) - 1);
	return list.getIndex($itemhunter_items, random_index);
}

function itemhunter.setNextItem() {
	$itemhunter_item = itemhunter.getRandomItem();
	itemhunter.setItemFrame($itemhunter_item);
	itemhunter.resetTimer();
}

function itemhunter.reward(player) {
	money.add(player, $itemhunter_reward);
}

function itemhunter.shopItem(player) {
	if($itemhunter_item == null) {
		return;
	}
	item = living.getHand(player);
	if(item.getType(item) == item.getType($itemhunter_item)) {
		player.removeItem(player, $itemhunter_item);
		itemhunter.reward(player);
		itemhunter.setNextItem();
	}
}