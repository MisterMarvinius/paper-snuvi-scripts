event.load("custom_command");
event.load("inv_click");
event.load("inv_close");
event.load("block_click");

prefix_shop = "§6Shop";
wall_signs_tag = block.getTag("minecraft:wall_signs");
invid_to_sign_loc = map.new();

msg.string("dev", "§bChestShops §rloaded.");
@main
wait();
ignoreGoto(event);
goto("main");

@inv_click
if(!string.contains(inv_title, "Shop")) {
	goto("main");
}
inv_id = inv.getId(inv);
if(inv_slot == 10) {
	map.remove(invid_to_sign_loc, inv_id);
	inv.close(player);
	goto("main");
}
sign_loc = map.get(invid_to_sign_loc, inv_id);
if(sign_loc == null) {
	inv.close(player);
	msg.prefix(player, prefix_shop, "Defect shop.");
	goto("main");
}
if(!isAWallSign(sign_loc)) {
	map.remove(invid_to_sign_loc, inv_id);
	inv.close(player);
	msg.prefix(player, prefix_shop, "No longer a shop.");
	goto("main");
}
factor = 1;
if(click == "SHIFT_LEFT") {
	factor = 8;
}
adminshop = isAdminShop(sign_loc);
buy_price = shop.getBuyPrice(inv) * factor;
sell_price = shop.getSellPrice(inv) * factor;
chestloc1 = loc.mod(sign_loc, 0, -1, 0);
chestloc2 = block.getSecChest(chestloc1);
if(!isAChest(chestloc1)) {
	map.remove(invid_to_sign_loc, inv_id);
	inv.close(player);
	msg.prefix(player, prefix_shop, "No longer a shop.");
	goto("main");
}
if(inv_slot == 3) { //Buy
	itemstack = inv.getItem(inv, 4);
	itemstack_2 = item.clone(itemstack);
	if(click == "SHIFT_LEFT") {
		item.setAmount(itemstack_2, item.getAmount(itemstack_2) * factor);
	}
	//Check players money
	if(!hasEnoughMoney(player, buy_price)) {
		msg.prefix(player, prefix_shop, "You do not have enough money.");
		goto("main");
	}
	if(!adminshop) {
		itemamount2 = 0;
		if(chestloc2 != null) {
			itemamount2 = block.getItemAmount(chestloc2, true, itemstack);
		}
		itemamount1 = block.getItemAmount(chestloc1, true, itemstack);
		if(itemamount1 + itemamount2 < item.getAmount(itemstack_2)) {
			msg.prefix(player, prefix_shop, "Not enough items in chest.");
			goto("main");
		}
	}
	//Items ins Inventar geben oder droppen
	notgiven = player.giveItem(player, itemstack_2);
	if(notgiven != null) {
		item.drop(entity.getLocation(player), notgiven);
	}
	//In einem AdminShop werden keine Items abgelegt
	if(!adminshop) {
		itemstack_temp = item.clone(itemstack_2);
		notremoved = block.subItem(chestloc1, itemstack_temp);
		if(chestloc2 != null) {
			block.subItem(chestloc2, notremoved);
		}
		owner_player_id = text.convert(block.getSign(sign_loc, 3));
		if(!isDouble(owner_player_id)) {
			msg.prefix(player, prefix_shop, "Invalid shop owner.");
			goto("main");
		}
		if(player.getNameFromId(owner_player_id) == null) {
			msg.prefix(player, prefix_shop, "Invalid shop owner.");
			goto("main");
		}
		addMoney(owner_player_id, buy_price);
	}
	subMoney(player, buy_price);
	inv.update(player);
}  elseif(inv_slot == 6) { //Sell
	itemstack = inv.getItem(inv, 5);
	itemstack_2 = item.clone(itemstack);
	if(click == "SHIFT_LEFT") {
		item.setAmount(itemstack_2, item.getAmount(itemstack_2) * factor);
	}
	if(!adminshop) {
		owner_player_id = text.convert(block.getSign(sign_loc, 3));
		if(!isDouble(owner_player_id)) {
			msg.prefix(player, prefix_shop, "Invalid shop owner.");
			goto("main");
		}
		if(player.getNameFromId(owner_player_id) == null) {
			msg.prefix(player, prefix_shop, "Invalid shop owner.");
			goto("main");
		}
		if(!hasEnoughMoney(owner_player_id, sell_price)) {
			msg.prefix(player, prefix_shop, "Shop owner does not have enough money.");
			goto("main");
		}
	}
	//Items des Spielers checken
	if(player.getItemAmount(player, true, itemstack) < item.getAmount(itemstack_2)) {
		msg.prefix(player, prefix_shop, "You do not have enough items.");
		goto("main");
	}
	//Platz in der Kiste checken
	if(!adminshop) {
		//Versuche den ganzen ItemStack in der 1.Kiste unterzubringen
		itemstack_temp = item.clone(itemstack_2);
		notinchest1 = block.addItem(chestloc1, itemstack_temp);
		if(item.getType(notinchest1) != "minecraft:air") {
			//Versuche den Rest des ItemStack in der 2.Kiste unterzubringen
			notinchest2 = block.addItem(chestloc2, notinchest1);
			if(item.getType(notinchest1) != "minecraft:air") {
				//Entferne die ItemStacks wieder
				msg.prefix(player, prefix_shop, "Not enough space in chest.");
				block.subItem(chestloc1, itemstack_temp);
				block.addItem(chestloc1, notinchest2);
				goto("main");
			}
		}
		subMoney(owner_player_id, sell_price);
	}
	addMoney(player, sell_price);
	player.removeItem(player, itemstack_2);
	inv.update(player);
}
goto("main");

@inv_close
map.remove(invid_to_sign_loc, inv_id);
goto("main");

@custom_command
if(!isPlayer(sender)) {
	goto("main");
}
player = sender;
if(command == "shop") {
	size = list.getSize(args);
	if(size == 0) {
		@syntax
		msg.prefix(player, prefix_shop, "/shop ...");
		msg.string(player, "§6 - create §rCreates a shop");
		msg.string(player, "§6 - buy <amount> <price> §rSet buy settings");
		msg.string(player, "§6 - sell <amount> <price> §rSet sell settings");
		msg.string(player, "§6 - remove <buy/sell> §rRemove settings");
		if(perm.has("adminshop", player)) {
			msg.string(player, "§6 - admin §rSet to adminshop");
		}
		goto("main");
	}
	arg0 = text.toLowerCase(list.getIndex(args, 0));
	if(arg0 == "create") {
		if(size != 1) {
			msg.prefix(player, prefix_shop, "§6/shop create");
			goto("main");
		}
		sign_loc = player.getTarget(player, 5, false, false);
		if(!isAWallSign(sign_loc)) {
			msg.prefix(player, prefix_shop, "Look at a sign.");
			goto("main");
		}
		chest_loc = loc.mod(sign_loc, 0, -1, 0);
		if(!isAChest(chest_loc)) {
			msg.prefix(player, prefix_shop, "No chest under sign.");
			goto("main");
		}
		if(!plot.check(sign_loc, player, 0, false)) {
			msg.prefix(player, prefix_shop, "Not your plot.");
			goto("main");
		}
		block.setSign(sign_loc, 0, "[Shop]");
		block.setSign(sign_loc, 3, text.number(player.getId(player)));
		msg.prefix(player, prefix_shop, "§rCreated Shop.");
		goto("main");
	}
	if(arg0 == "admin") {
		if(!perm.has("adminshop", player)) {
			msg.prefix(player, prefix_shop, "§6No Permission.");
			goto("main");
		}
		if(size != 1) {
			msg.prefix(player, prefix_shop, "§6/shop admin");
			goto("main");
		}
		sign_loc = player.getTarget(player, 5, false, false);
		if(!isAWallSign(sign_loc)) {
			msg.prefix(player, prefix_shop, "Look at a sign.");
			goto("main");
		}
		chest_loc = loc.mod(sign_loc, 0, -1, 0);
		if(!isAChest(chest_loc)) {
			msg.prefix(player, prefix_shop, "No chest under sign.");
			goto("main");
		}
		block.setSign(sign_loc, 0, "[Admin Shop]");
		block.setSign(sign_loc, 3, "");
		msg.prefix(player, prefix_shop, "§rCreated Admin Shop.");
		goto("main");
	}
	if(arg0 == "buy" || arg0 == "sell") {
		if(size != 3) {
			msg.prefix(player, prefix_shop, string.concat("§6/shop ", arg0, " <amount> <price>"));
			goto("main");
		}
		amount = list.getIndex(args, 1);
		price = list.getIndex(args, 2);
		if(!isDouble(amount) || !isDouble(amount)) {
			msg.prefix(player, prefix_shop, "§rNumber expected.");
			goto("main");
		}
		if(price < 0) {
			msg.prefix(player, prefix_shop, "§rPositive number expected.");
			goto("main");
		}
		sign_loc = player.getTarget(player, 5, false, false);
		if(!isAWallSign(sign_loc)) {
			msg.prefix(player, prefix_shop, "Look at a sign.");
			goto("main");
		}
		chest_loc = loc.mod(sign_loc, 0, -1, 0);
		if(!isAChest(chest_loc)) {
			msg.prefix(player, prefix_shop, "No chest under sign.");
			goto("main");
		}
		if(!shop.isOwner(player, sign_loc)) {
			msg.prefix(player, prefix_shop, "You are not the owner.");
			goto("main");
		}
		if(arg0 == "buy") {
			block.setSign(sign_loc, 1, string.concat("Buy ", text.number(amount), " for ", text.number(price)));
		} else {
			block.setSign(sign_loc, 2, string.concat("Sell ", text.number(amount), " for ", text.number(price)));
		}
		msg.prefix(player, prefix_shop, "§rPrice set.");
		goto("main");
	}
	if(arg0 == "remove") {
		if(size != 2) {
			msg.prefix(player, prefix_shop, "§6/shop remove <buy/sell>");
			goto("main");
		}
		sign_loc = player.getTarget(player, 5, false, false);
		if(!isAWallSign(sign_loc)) {
			msg.prefix(player, prefix_shop, "Look at a sign.");
			goto("main");
		}
		chest_loc = loc.mod(sign_loc, 0, -1, 0);
		if(!isAChest(chest_loc)) {
			msg.prefix(player, prefix_shop, "No chest under sign.");
			goto("main");
		}
		if(!shop.isOwner(player, sign_loc)) {
			msg.prefix(player, prefix_shop, "You are not the owner.");
			goto("main");
		}
		arg1 = text.toLowerCase(list.getIndex(args, 1));
		if(arg1 == "buy") {
			block.setSign(sign_loc, 1, "");
		} elseif(arg1 == "sell") {
			block.setSign(sign_loc, 2, "");
		} else {
			msg.prefix(player, prefix_shop, "§6/shop remove <buy/sell>");
			goto("main");
		}
		msg.prefix(player, prefix_shop, "§rPrice removed.");
		goto("main");
	}
	goto("syntax");
}
goto("main");

@block_click
if(block == null) {
	goto("main");
}
if(block.isWallSign(block)) {
	if(!block.isShopSign(block)) {
		goto("main");
	}
	chest_block = block.getShopChest(block);
	if(chest_block == null) {
		goto("main");
	}
	owner_id = shop.getOwner(block);
	if(owner_id == null) {
		inv_title = "Admin Shop";
	} else {
		owner_name = player.getNameFromId(owner_id);
		if(owner_name == null) {
			msg.prefix(player, prefix_shop, "Invalid shop owner.");
			goto("main");
		}
		inv_title = string.concat("§8Shop of ", owner_name);
	}
	item_stack = shop.getItem(chest_block);
	if(item_stack == null) {
		msg.prefix(player, prefix_shop, "No item found.");
		goto("main");
	}
	inv = inv.new("222320000002322203", inv_title);
	shop.setBuyElement(inv, block, item.clone(item_stack));
	shop.setSellElement(inv, block, item.clone(item_stack));
	inv.setItem(inv, 10, read.item("km:cross_red", 1, "§rCancel"));
	inv.open(inv, player);
	map.add(invid_to_sign_loc, inv.getId(inv), sign_loc);
}
goto("main");

function shop.setBuyElement(sign_block, inv, item_stack) {
	buy_line = block.getSign(sign_block, 1);
	split_list = string.split(" ", buy_line);
	buy_amount = list.getIndex(split_list, 1);
	if(!isDouble(buy_amount)) {
		return;
	}
	buy_price = list.getIndex(split_list, 3);
	if(!isDouble(buy_price)) {
		return;
	}
	item.setAmount(item_stack, buy_amount);
	a = money.split(buy_price);
	gold = a[0];
	silver = a[1];
	bronze = a[2];
	index = 2;
	if(bronze != 0) {
		inv.setItem(inv, index--, read.item("km:coin_copper", bronze));
	}
	if(silver != 0) {
		inv.setItem(inv, index--, read.item("km:coin_silver", silver));
	}
	if(gold != 0) {
		inv.setItem(inv, index, read.item("km:coin_gold", gold));
	}
	inv.setItem(inv, 3, read.item("km:arrow_right", 1, "§rBuy"));
	inv.setItem(inv, 4, item_stack);
}

function shop.setSellElement(sign_block, inv, item_stack) {
	sell_line = block.getSign(sign_block, 2);
	split_list = string.split(" ", sell_line);
	sell_amount = list.getIndex(split_list, 1);
	if(!isDouble(sell_amount)) {
		return;
	}
	sell_price = list.getIndex(split_list, 3);
	if(!isDouble(sell_price)) {
		return;
	}
	sell_stack = item.clone(item_stack);
	item.setAmount(sell_stack, sell_amount);
	a = money.split(sell_price);
	gold = a[0];
	silver = a[1];
	bronze = a[2];
	index = 7;
	if(bronze != 0) {
		inv.setItem(inv, index++, read.item("km:coin_copper", bronze));
	}
	if(silver != 0) {
		inv.setItem(inv, index++, read.item("km:coin_silver", silver));
	}
	if(gold != 0) {
		inv.setItem(inv, index, read.item("km:coin_gold", gold));
	}
	inv.setItem(inv, 5, sell_stack);
	inv.setItem(inv, 6, read.item("km:arrow_right", 1, "§rSell"));
}


function shop.getItem(chest_block) {
	inv = block.getInventory(chest_block);
	air = item.getAir();
	for(i = 0; i < inv.getSize(inv); i++) {
		item_stack = inv.getItem(inv, i);
		if(item.getType(item_stack) != air) {
			return item.clone(item_stack);
		}
	}
	return null;
}

function block.isChest(block) {
	return block.isType(block, "minecraft:chest");
}

function block.isWallSign(block) {
	if(block == null) {
		return false;
	}
	return block.hasTag($wall_signs_tag, block);
}

function block.getSignAbove(block) {
	sign_loc = loc.mod(block.getLocation(block), 0, 1, 0);
	sign_block = block.get(sign_loc);
	if(block.isAWallSign(sign_block)) {
		return sign_block;
	}
	return null;
}

function block.getSecChest(block) {
	loc = block.getLocation(block);
	mod_block = block.get(loc.mod(loc, 1, 0, 0));
	if(block.isChest(mod_block)) {
		return mod_block;
	}
	mod_block = block.get(loc.mod(loc, 0, 0, 1));
	if(block.isChest(mod_block)) {
		return mod_block;
	}
	mod_block = block.get(loc.mod(loc, -1, 0, 0));
	if(block.isChest(mod_block)) {
		return mod_block;
	}
	mod_block = block.get(loc.mod(loc, 0, 0, -1));
	if(block.isChest(mod_block)) {
		return mod_block;
	}
	return null;
}

function block.getShopChest(sign_block) {
	sign_loc = block.getLocation(sign_block);
	chest_loc = loc.mod(sign_loc, 0, -1, 0);
	chest_block = block.get(chest_loc);
	if(block.isChest(chest_block)) {
		return chest_block;
	}
	return null;
}

function block.getShopSign(chest_block) {
	sign_block == block.getSignAbove(chest_block);
	if(sign_block == null) {
		chest_block = block.getSecChest(chest_block);
		sign_block == block.getSignAbove(chest_block);
		if(sign_block == null) {
			return null;
		}
	}
	if(block.isShopSign(sign_block)) {
		return sign_block;
	}
	return null;
}

function block.isShopSign(sign_block) {
	line0 = string.text(block.getSign(sign_block, 0));
	if(line0 == "[Shop]") {
		return true;
	}
	if(line0 == "[Admin Shop]") {
		return true;
	}
	return false;
}

function shop.getOwner(sign_block) {
	owner_id = read.number(block.getSign(sign_block, 3));
	if(owner_id == null) {
		return null;
	}
	return owner_id;
}

function shop.isOwner(player, sign_block) {
	if(player.hasBypass(player)) {
		return true;
	}
	owner_id = read.number(block.getSign(sign_block, 3));
	if(owner_id == null) {
		return perm.has("adminshop", player);
	}
	owner_name = player.getNameFromId(owner_id);
	if(owner_name == null) {
		return false;
	}
	return owner_id == player.getId(player);
}

function shop.getItemAmount(item) {
	if(item.getType(item) == "km:coin_gold") {
		return item.getAmount(item) * 64 * 64;
	}
	if(item.getType(item) == "km:coin_silver") {
		return item.getAmount(item) * 64;
	}
	if(item.getType(item) == "km:coin_copper") {
		return item.getAmount(item);
	}
	return 0;
}

function shop.getBuyPrice(inv) {
	snuvis = 0;
	snuvis += shop.getItemAmount(inv.getItem(inv, 0));
	snuvis += shop.getItemAmount(inv.getItem(inv, 1));
	snuvis += shop.getItemAmount(inv.getItem(inv, 2));
	return snuvis;
}

function shop.getSellPrice(inv) {
	snuvis = 0;
	snuvis += shop.getItemAmount(inv.getItem(inv, 7));
	snuvis += shop.getItemAmount(inv.getItem(inv, 8));
	snuvis += shop.getItemAmount(inv.getItem(inv, 9));
	return snuvis;
}