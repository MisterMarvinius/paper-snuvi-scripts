print("test");

wand = material.get("WOODEN_AXE");
main_hand = read.slot("HAND");

event.load("custom_command");
event.load("block_click");
command.add("/set");

pos1 = map.new();
pos2 = map.new();

loc1 = null;
loc2 = null;


help = command.newhelp("/set", "/set");
command.addHelpChild(help, command.newHelpString("block_data", true));
command.addHelp(help);
command.sendhelp();

function addPos1(loc) {
    map.add($pos1, player.getUUID($player), loc);
}

function addPos2(loc) {
    map.add($pos2, player.getUUID($player), loc);
}

function send(msg) {
    msg($player, text.new(string.concat("[§6WorldEdit§r] ", msg)));
}

function checkMarkings() {
    uuid = player.getUUID($player);
    $loc1 = map.get($pos1, uuid);
    $loc2 = map.get($pos2, uuid);
    if($loc1 == null || $loc2 == null) {
        send("Missing position");
        return true;
    }
    if(loc.distance($loc1, $loc2) > 100) {
        send("Too much distance");
        return true;
    }
    loc.sort($loc1, $loc2);
    return false;
}

function getData(raw_data_string) {
    data_string = raw_data_string;
    if(!string.startsWith(data_string, "minecraft:", 0)) {
        data_string = string.concat("minecraft:", data_string);
    }
    data = read.blockData(data_string);
    if(data == null) {
        send(string.concat("'", raw_data_string, "' is not valid blockdata."));
    }
    return data;
}

// event loop
@loop
wait();
igoto(event);
goto("loop");

@block_click
if(block != null && hand == main_hand && perm.has("worldedit", player)) {
    item = living.getEquip(player, main_hand);
    if(item.getType(item) == wand) {
        if(action == "LEFT_CLICK_BLOCK") {
            loc = block.getLocation(block);
            addPos1(loc);
            send(string.concat("§6Pos 1: §r", 
                string.number(loc.getBlockX(loc)), "§6 | §r", 
                string.number(loc.getBlockY(loc)), "§6 | §r", 
                string.number(loc.getBlockZ(loc))
                ));
            cancel = true;
        } elseif(action == "RIGHT_CLICK_BLOCK") {
            loc = block.getLocation(block);
            addPos2(loc);
            send(string.concat("§6Pos 2: §r", 
                string.number(loc.getBlockX(loc)), "§6 | §r", 
                string.number(loc.getBlockY(loc)), "§6 | §r", 
                string.number(loc.getBlockZ(loc))
                ));
            cancel = true;
        }
    }
}
goto("loop");

@custom_command
if(!isPlayer(sender)) {
    msg(sender, "§cWorld Edit commands must be used by a player.");
    goto("loop");
}
player = sender;
if(string.startsWith(command, "/", 0)) {
    igoto(string.substring(command, 1, string.length(command)));
}
goto("loop");

@set
if(checkMarkings()) {
    goto("loop");
}
if(list.getSize(args) <= 0) {
    send("//set <type>");
    goto("loop");
}
block_data = getData(list.get(args, 0));
if(block_data == null) {
    goto("loop");
}
iter = loc.iterator(loc1, loc2);
while(hasNext(iter)) {
    loc = next(iter);
    block.setData(block.get(loc), block_data);
}
goto("loop");

