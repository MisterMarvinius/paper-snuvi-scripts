event.load("animal_tame");
event.load("living_hurt");
event.load("living_heal");
event.load("entity_join");

entities_list = list.new();

permanent_name = set.new();
set.add(permanent_name, "villager");
set.add(permanent_name, "wandering_trader");

permanent_tamed_name = set.new();
set.add(permanent_tamed_name, "wolf");
set.add(permanent_tamed_name, "cat");
set.add(permanent_tamed_name, "horse");
set.add(permanent_tamed_name, "donkey");
set.add(permanent_tamed_name, "mule");
set.add(permanent_tamed_name, "skeleton_horse");
set.add(permanent_tamed_name, "zombie_horse");
set.add(permanent_tamed_name, "parrot");

msg("dev", "§bDamage §rloaded.");
@wait
wait();
ignoreGoto(event);
goto("wait");

@animal_tame
if(isLiving(animal) && !isPlayer(animal)) {
	if(list.getSize(entities_list) == 0) {
		sgoto(1, "sgoto_updateEntityName");
	}
	list.add(entities_list, animal);
}
goto("wait");

@entity_join
if(isLiving(entity) && !isPlayer(entity)) {
	updateEntityName(entity);
}
goto("wait");

@living_heal
cancel = true;
if(heal_amount <= 0) {
	goto("wait");
}
health = living.getHealth(living_entity);
if(health <= 0) {
	goto("wait");
}
new_health = health + heal_amount;
living.setHealth(living_entity, new_health);
if(isLiving(living_entity) && !isPlayer(living_entity)) {
	updateEntityName(living_entity);
}
goto("wait");

@living_hurt
if(isPlayer(living_entity) || !isLiving(living_entity)) {
	goto("wait");
}
if(list.getSize(entities_list) == 0) {
	sgoto(1, "sgoto_updateEntityName");
}
list.add(entities_list, living_entity);
goto("wait");

@sgoto_updateEntityName
iter = list.iterator(entities_list);
while(hasNext(iter)) {
	updateEntityName(next(iter));
}
list.clear(entities_list);
goto("wait");

function updateEntityName(living_entity) {
	if(!isLiving(living_entity)) {
		return;
	}
	entity_type = entity.getType(living_entity);
	if(entity_type == "nobody" || entity_type == "armor_stand" || entity_type == "human") {
		return;
	}

	entity_name = entity.getName(living_entity);
	max_health = living.getMaxHealth(living_entity);
	health = text.number(math.round(living.getHealth(living_entity)));
	max_health = text.number(math.roundComma(max_health, 1));
	
	heart_index = text.indexOf(entity_name, "❤", 0);
	if(heart_index == -1) {
		//Kein Herz gefunden
		setEntityName(living_entity, health, max_health, entity_name);
		return;
	}
		
	next_new_line = text.indexOf(entity_name, "\n", heart_index);
	if(next_new_line == -1) {
		//Rechts vom Herz keine New-Line gefunden
		setEntityName(living_entity, health, max_health, null);
		return;
	}
	entity_name = text.subString(entity_name, next_new_line + 1, text.length(entity_name));
	setEntityName(living_entity, health, max_health, entity_name);
}

function setEntityName(living_entity, health, max_health, entity_name) {
	entity_type = entity.getType(living_entity);
	permanent = false;
	if(set.contains($permanent_name, entity_type)) {
		permanent = true;
	} elseif(set.contains($permanent_tamed_name, entity_type)) {
		if(pet.isTamed(living_entity)) {
			permanent = true;
		}
	}
	if(entity_name == null) {
		entity.setName(living_entity, concat(health, "/", max_health, " §c❤"), permanent);
		return;
	}
	entity.setName(living_entity, concat(health, "/", max_health, " §c❤\n", entity_name), permanent);
}