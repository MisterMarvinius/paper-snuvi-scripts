//Schwarz muss IMMER nach Süden schauen!!!	POS Z
//Weiß muss IMMER nach Norden schauen!!!	NEG Z

rankingtable = "dameranks";
gamename = "§eCheckers";
game_short = "checkers";
game_tab = "§eCHECKERS";

player_team = map.new(); //player_uuid -> team
team_player = map.new(); //team -> player_uuid
block_click_catcher = set.new();
pointed_loc = loc.new(gamesworld, 0, 0, 0); //Default-Wert, um Error zu verhindern

air_mat = material.getAir();
air_data = read.blockData("air");
banners_tag = block.getTag("minecraft:banners");

colorcode = map.new();
map.add(colorcode, 0, "§0");
map.add(colorcode, 1, "§7");

minigame.initStart();
goto("simplelobby");

@finalstart
player_list = minigame.getPlayers(script_id);
minigame.speakAll(gamename, "The game has started.");

black_token = material.get("wither_skeleton_skull");
white_token = material.get("skeleton_skull");
black_token_dame = material.get("black_banner");
white_token_dame = material.get("white_banner");

black_token_data = read.blockData("wither_skeleton_skull[rotation=8]");
white_token_data = read.blockData("skeleton_skull[rotation=0]");
black_token_dame_data = read.blockData("black_banner[rotation=0]");
white_token_dame_data = read.blockData("white_banner[rotation=8]");

tokens_data = map.new();
map.add(tokens_data, 0, black_token_data);
map.add(tokens_data, 1, white_token_data);

tokens = map.new();
map.add(tokens, 0, black_token);
map.add(tokens, 1, white_token);

dame_tokens_data = map.new();
map.add(dame_tokens_data, 0, black_token_dame_data);
map.add(dame_tokens_data, 1, white_token_dame_data);

dame_tokens = map.new();
map.add(dame_tokens, 0, black_token_dame);
map.add(dame_tokens, 1, white_token_dame);

resetGameField();

for(i = 0; i < list.getSize(player_list); i++) {
	//prepare players
	p_uuid = list.getIndex(player_list, i);
	p = player.get(p_uuid);
	entity.teleport(p, map.get(spawn_locs, i));
	player.setGamemode(p, "SURVIVAL");
	player.setFly(p, true);
	//assign team
	map.add(player_team, p_uuid, i);
	map.add(team_player, i, p_uuid);
	color = map.get(colorcode, i);
	minigame.setTabName(p, game_tab, color);
}

setNextTeam();

@wait
wait();
if(event == "entity_damage") {
	if(!isPlayer(entity)) {
		goto("wait");
	}
	player = entity;
}
if(player.hasMinigameId(player, script_id)) {
	ignoreGoto(event);
}
goto("wait");

@block_click
if(slot.isOffHand(hand)) {
	goto("wait");
}
team = getTeamFromPlayer(player);
if(action != "RIGHT_CLICK_BLOCK") {
	goto("wait");
}
if(!isNextTeam(player)) {
	msgTeam(team, "It's not your turn.");
	goto("wait");
}
if(isTeamToken(team, block)) {
	if(!can_select_other_token && block != selected_token) {
		msgTeam(team, "Cannot toggle game piece in the middle of a move.");
		goto("wait");
	}
	selected_token = block;
	setPointBlock(block.mod(selected_token, 0, -1, 0));
	goto("wait");
}
if(selected_token == null) {
	msgTeam(team, "Choose one of your game pieces to move.");
	goto("wait");
}
if(!isValidGameField(block)) {
	msgTeam(team, "Not a valid move.");
	goto("wait");
}
b = block.mod(block, 0, 1, 0);
if(!isValidMove(selected_token, b, team)) {
	goto("wait");
}
doMove(team, selected_token, b);
if(checkWinCondition(team)) {
	winner = player;
	goto("win");
}
//if a hit was done ant a further hit is possible, than continue move, otherwise nextplayer
if(isAHit && canTokenHit(team, b)) {
	msgTeam(team, "Further hit is possible.");
	selected_token = b;
	setPointBlock(block.mod(selected_token, 0, -1, 0));
	goto("wait");
}
setNextTeam();
goto("wait");

function setPointBlock(b) {
	resetPointBlock();
	$point_block = b;
	block.setMaterial($point_block, $point_mat);
}

function resetPointBlock() {
	if($point_block != null) {
		block.setMaterial($point_block, $game_field_mat);
	}
}

function doMove(team, from_block, to_block) {
	$can_select_other_token = false;
	
	//set target token
	//check if opp side was reached -> upgrade to dame
	to_z = loc.getZ(block.getLocation(to_block));
	if(team == 0 && to_z == $upper_z || team == 1 && to_z == $lower_z) {
		token_data = getDameTokenData(team);
	} elseif(isDameToken(from_block)) {
		token_data = getDameTokenData(team);
	} else {
		token_data = getTokenData(team);
	}
	block.setData(to_block, token_data);
	
	//remove source token
	block.setMaterial(from_block, $air_mat);
	
	//hit_token is removed already somewhere else
}

@block_break
@block_place
@entity_damage
cancel = true;
goto("wait");

@win
resetGameField();
script = script.getFromId(script_id);
minigame.speakAll(gamename, string.concat("§6", player.getName(winner), " §rhas won."));
for(i = 0; i < list.getSize(player_list); i++) {
	p = player.get(list.getIndex(player_list, i));
	if(p == winner) {
		showstats(p, true);
	} else {
		showstats(p, false);
	}
	player.setFly(p, false);
}
minigame.kickAllPlayers(script);
minigame.term(script, gamesignloc);
term();

@player_logout
@player_giveup
minigame.speakAll(gamename, string.concat("§8", player.getName(player), " §ehas left the game."));
player.setFly(player, false);
showstats(player, false);
script = script.getFromId(script_id);
minigame.kickPlayer(script, player);
size = list.getSize(player_list);
if(size < minplayers) {
	winner = player.get(list.getIndex(player_list, 0));
	goto("win");
}
team = map.get(player_team, player.getUuid(player));
token = map.get(tokens_map, team);
goto("wait");

function setNextTeam() {
	if($next_team == null) {
		//init at start
		$next_team = math.random(0, 1);
	} else {
		//set next during game
		$next_team++;
		if($next_team >= 2) {
			$next_team = 0;
		}
	}
	
	//announce
	next_team_name = getTeamName($next_team);
	if(string.endsWith(next_team_name, "s")) {
		minigame.speakAll($gamename, string.concat("It's ", next_team_name, "' §rturn."));
	} else {
		minigame.speakAll($gamename, string.concat("It's ", next_team_name, "'s §rturn."));
	}
	next_player = getPlayerFromTeam($next_team);
	sound.spawnForPlayer(next_player, $pling_sound, $sound_category_ambient);
	
	resetPointBlock();
	$selected_token = null;
	$can_select_other_token = true;
	$isAHit = false;
}

function showstats(player, won) { //Player player, Boolean won
	player_id = player.getId(player);
	last_record = ranking.getPoints($rankingtable, player_id);
	if(won) {
		last_record++;
	}
	playedgames = minigame.getPlayed(player_id, $game_short) + 1;
	minigame.setPlayed(player_id, $game_short, playedgames);
	ranking.setPoints($rankingtable, player_id, last_record);
	
	minigame.statsHeader(player, $gamename, "§e");
	minigame.statsLine(player, "§e", "Won games", string.number(last_record));
	minigame.statsLine(player, "§e", "Played games", string.number(playedgames));
	if(playedgames != 0) {
		minigame.statsLine(player, "§e", "Win ratio", string.concat(string.number(math.roundComma((last_record / playedgames) * 100, 2)), "%"));
	}
}

function resetGameField() {
	resetPointBlock();
	loc = loc.mod($black_left_front_corner_loc, 0, 0, 0);
	resetFieldRow(loc, $black_token_data);
	loc.add(loc, -1, 0, 1);
	resetFieldRow(loc, $black_token_data);
	loc.add(loc, 1, 0, 1);
	resetFieldRow(loc, $black_token_data);
	loc.add(loc, -1, 0, 1);
	resetFieldRow(loc, $air_data);
	loc.add(loc, 1, 0, 1);
	resetFieldRow(loc, $air_data);
	loc.add(loc, -1, 0, 1);
	resetFieldRow(loc, $white_token_data);
	loc.add(loc, 1, 0, 1);
	resetFieldRow(loc, $white_token_data);
	loc.add(loc, -1, 0, 1);
	resetFieldRow(loc, $white_token_data);
}

function resetFieldRow(loc, token_data) {
	x = 0;
	for(i = 0; i < 4; i++) {
		block = block.get(loc);
		block.setData(block, token_data);
		loc.add(loc, -2, 0, 0);
	}
	loc.add(loc, 8, 0, 0);
}

function msgTeam(team, message) {
	player = getPlayerFromTeam(team);
	msg.prefix(player, $gamename, message);
}

function getTeamFromPlayer(player) {
	return map.get($player_team, player.getUuid(player));
}

function getPlayerFromTeam(team) {
	p_uuid = map.get($team_player, team);
	return player.get(p_uuid);
}

function getColorCode(team) {
	return map.get($colorcode, team);
}

function getTeamName(team) {
	color = getColorCode(team);
	p_name = player.getName(getPlayerFromTeam(team));
	return string.concat(color, p_name);
}

function isNextTeam(player) {
	return getTeamFromPlayer(player) == $next_team;
}

function isTeamToken(team, block) {
	own_mat = getTokenMat(team);
	own_dame_mat = getDameTokenMat(team);
	block_mat = block.getType(block);
	return own_mat == block_mat || own_dame_mat == block_mat;
}

function isDameToken(block) {
	return block.hasTag(block, $banners_tag);
}

function getTokenMat(team) {
	return map.get($tokens, team);
}

function getTokenData(team) {
	return map.get($tokens_data, team);
}

function getDameTokenMat(team) {
	return map.get($dame_tokens, team);
}

function getDameTokenData(team) {
	return map.get($dame_tokens_data, team);
}

function isValidGameField(block) {
	loc = block.getLocation(block);
	x = loc.getX(loc);
	y = loc.getY(loc);
	z = loc.getZ(loc);
	if(y != $y_level_board) {
		return false;
	}
	if(x < $lower_x || x > $upper_x) {
		return false;
	}
	if(z < $lower_z || z > $upper_z) {
		return false;
	}
	if(block.getType(block) != $game_field_mat) {
		return false;
	}
	return true;
}

//selected_token and to_block are on y_level_token
function isValidMove(selected_token, to_block, team) {
	//target field needs to be empty
	if(block.getType(to_block) != $air_mat) {
		msgTeam(team, "Not a valid move.");
		return false;
	}
	
	//check direction
	source_loc = block.getLocation(selected_token);
	source_x = loc.getX(source_loc);
	source_z = loc.getZ(source_loc);
	target_loc = block.getLocation(to_block);
	target_x = loc.getX(target_loc);
	target_z = loc.getZ(target_loc);
	diff_x = target_x - source_x;
	diff_z = target_z - source_z;
	if(!isDameToken(selected_token)) {
		//black team (0) has to move towords pos z
		if(team == 0 && diff_z <= 0) {
			msgTeam(team, "Not a valid move.");
			return false;
		}
		//white team (1) has to move towords neg z
		if(team == 1 && diff_z >= 0) {
			msgTeam(team, "Not a valid move.");
			return false;
		}
	}
	
	//it has to be a move or a hit
	if(math.abs(diff_x) == 1 && math.abs(diff_z) == 1) {
		//if its a move, but a hit would be possible, hit has to be done
		if(canHitOpponent(team)) {
			msgTeam(team, "Not a valid move. If you can hit an opponents game piece you have to.");
			return false;
		}
		return true;
	} elseif(math.abs(diff_x) == 2 && math.abs(diff_z) == 2) {
		//hit token cannot be air and not your own token
		hit_block = block.mod(selected_token, diff_x / 2, 0, diff_z / 2);
		if(block.getType(hit_block) == $air_mat) {
			msgTeam(team, "Not a valid move.");
			return false;
		}
		if(isTeamToken(team, hit_block)) {
			msgTeam(team, "Not a valid move.");
			return false;
		}
		$isAHit = true;
		block.setMaterial(hit_block, $air_mat);
		return true;
	}
	msgTeam(team, "Not a valid move.");
	return false;
}

//from_block and to_block are on y_level_token
function isValidHitMove(from_block, to_block, opp) {
	if(!isValidGameField(block.mod(to_block, 0, -1, 0))) {
		return false;
	}
	//target field needs to be empty
	if(block.getType(to_block) != $air_mat) {
		return false;
	}
	//hit block has to be opp
	source_loc = block.getLocation(from_block);
	source_x = loc.getX(source_loc);
	source_z = loc.getZ(source_loc);
	target_loc = block.getLocation(to_block);
	target_x = loc.getX(target_loc);
	target_z = loc.getZ(target_loc);
	diff_x = target_x - source_x;
	diff_z = target_z - source_z;
	hit_block = block.mod(from_block, diff_x / 2, 0, diff_z / 2);
	return isTeamToken(opp, hit_block);
}

function canTokenHit(team, block) {
	if(team == 0) {
		opp = 1;
		z_multiplier = 1; //black team (0) has to move towords pos z
	} else {
		opp = 0;
		z_multiplier = -1; //white team (1) has to move towords neg z
	}
	loc = block.getLocation(block);
	//check for both forward direction
	to_block = block.mod(block, 2, 0, 2 * z_multiplier);
	if(isValidHitMove(block, to_block, opp)) {
		return true;
	}
	to_block = block.mod(block, -2, 0, 2 * z_multiplier);
	if(isValidHitMove(block, to_block, opp)) {
		return true;
	}
	if(isDameToken(block)) {
		//check also for both backwards directions
		to_block = block.mod(block, 2, 0, -2 * z_multiplier);
		if(isValidHitMove(block, to_block, opp)) {
			return true;
		}
		to_block = block.mod(block, -2, 0, -2 * z_multiplier);
		if(isValidHitMove(block, to_block, opp)) {
			return true;
		}
	}
	return false;
}

function getOpponentTeam(team) {
	if(team == 0) {
		return 1;
	}
	return 0;
}

function canHitOpponent(team) {
	l = getTokensOnBoard(team);
	iter = iterator(l);
	while(hasNext(iter)) {
		b = next(iter);
		if(canTokenHit(team, b)) {
			return true;
		}
	}
	return false;
}

function getTokensOnBoard(team) {
	l = list.new();
	for(x = $lower_x; x <= $upper_x; x++) {
		for(z = $lower_z; z <= $upper_z; z++) {
			loc = loc.new($gamesworld, x, $y_level_token, z);
			b = block.get(loc);
			if(isTeamToken(team, b)) {
				list.add(l, b);
			}
		}
	}
	return l;
}

function checkWinCondition(team) {
	opp = getOpponentTeam(team);
	return list.getSize(getTokensOnBoard(opp)) == 0;
}