event.load("block_click");


doors_tag = block.getTag("minecraft:wooden_doors");
trapdoors_tag = block.getTag("minecraft:wooden_trapdoors");

door_loc_1 = loc.new(world.get("world"), 107, 56, 15);
door_loc_2 = loc.mod(door_loc_1, 0, -3, 0);
door_loc_3 = loc.mod(door_loc_2, 0, -5, 0);
door_loc_4 = loc.mod(door_loc_3, 0, -3, 0);
door_loc_5 = loc.mod(door_loc_1, 0, 5, 0);

secret_doors = list.new();

list.add(secret_doors, door_loc_1);
list.add(secret_doors, door_loc_2);
list.add(secret_doors, door_loc_3);
list.add(secret_doors, door_loc_4);
list.add(secret_doors, door_loc_5);

open_door_sound = sound.get("BLOCK_WOODEN_DOOR_OPEN");
close_door_sound = sound.get("BLOCK_WOODEN_DOOR_CLOSE");

@main
wait();
loc = entity.getLocation(player);

if(block == null) {
	goto("main");
}
block_loc = block.getLocation(block);

if (action != "RIGHT_CLICK_BLOCK" || cancel){
	goto("main");
}

if(block.isDoor(block)) {
	goto("door");
}
elseif(block.isTrapdoor(block)) {
	goto("trapdoor");
}
goto("main");


@trapdoor

open_value = block.isOpen(block);
state_facing = block.getDirectionalFace(block);
trapdoors = list.new();

block_loc = block.getLocation(block);
list.add(trapdoors, block_loc);

if(state_facing == "NORTH" || state_facing == "SOUTH") {
	trapdoor_loc_2 = loc.mod(block_loc, -1, 0, 0);
	trapdoor_loc_3 = loc.mod(block_loc, 1, 0, 0);
			
	if (state_facing == "NORTH") {
		opposite_loc = loc.mod(block_loc, 0, 0, -1);
	} else {
		opposite_loc = loc.mod(block_loc, 0, 0, 1);
	}			
} elseif(state_facing == "EAST" || state_facing == "WEST") {
	trapdoor_loc_2 = loc.mod(block_loc, 0, 0, -1);
	trapdoor_loc_3 = loc.mod(block_loc, 0, 0, 1);
			
	if (state_facing == "EAST"){
		opposite_loc = loc.mod(block_loc, 1, 0, 0);
	} else {
		opposite_loc = loc.mod(block_loc, -1, 0, 0);
	}
}

block2 = block.get(trapdoor_loc_2);
block3 = block.get(trapdoor_loc_3);
block4 = block.get(opposite_loc);

if (isNeighbouring_Trapdoor(block2, state_facing)){ list.add(trapdoors, trapdoor_loc_2); }
if (isNeighbouring_Trapdoor(block3, state_facing)){ list.add(trapdoors, trapdoor_loc_3); }

if ( isOpposite_Trapdoor(block4, state_facing)){
	list.add(trapdoors, opposite_loc);
	state_facing2 = block.getDirectionalFace(block4);
	
	if(state_facing == "NORTH" || state_facing == "SOUTH") {
			ntrapdoor_loc_2 = loc.mod(opposite_loc, -1, 0, 0);
			ntrapdoor_loc_3 = loc.mod(opposite_loc, 1, 0, 0);
	}elseif(state_facing == "EAST" || state_facing == "WEST") {
			ntrapdoor_loc_2 = loc.mod(opposite_loc, 0, 0, -1);
			ntrapdoor_loc_3 = loc.mod(opposite_loc, 0, 0, 1);
	}
	nblock2 = block.get(ntrapdoor_loc_2);
	nblock3 = block.get(ntrapdoor_loc_3);
	if (isNeighbouring_Trapdoor(nblock2, state_facing2)){ list.add(trapdoors, ntrapdoor_loc_2); }
	if (isNeighbouring_Trapdoor(nblock3, state_facing2)){ list.add(trapdoors, ntrapdoor_loc_3); }
}

door.openlist(trapdoors, open_value);
goto("main");


@door
if (list.contains(secret_doors, block_loc) || list.contains(secret_doors, loc.mod(block_loc, 0, -1, 0))){
	cancel = true;
	open_value = block.isOpen(block);
	door.openlist(secret_doors, open_value);
	goto("main");
}

goto("main");

function isNeighbouring_Trapdoor(block, state_facing){
	if(block.isTrapdoor(block)){
		//msg("dev", text.merge(text.new("N"), text.new(state_facing), text.new(block.getDirectionalFace(block))));
		return block.getDirectionalFace(block) == state_facing;
	}
	return false;
}

function isOpposite_Trapdoor(block, state_facing){
	if(!block.isTrapdoor(block)){
		return false;
	}
	state_facing2 = block.getDirectionalFace(block);
	if (state_facing == "NORTH" && state_facing2 == "SOUTH" || state_facing == "SOUTH" && state_facing2 == "NORTH"){
		return true;
	}
	if (state_facing == "EAST" && state_facing2 == "WEST" || state_facing == "WEST" && state_facing2 == "EAST"){
		return true;
	}
	//msg("dev", text.merge(text.new("O"), text.new(state_facing), text.new(state_facing2)));
		
	return false;
}

function door.openlist(doors_list, open_value){
	list_it = iterator(doors_list);
	//msg("dev",text.new(doors_list));
	
	while (hasNext(list_it)){
		door_loc = next(list_it);
		block2 = block.get(door_loc);
		open_value_2 = block.isOpen(block2);
		
		if(open_value && open_value_2) {
			door.open(block2, door_loc, false);
		} elseif(!open_value && !open_value_2) {
			door.open(block2, door_loc, true);
		}
	}
}

function door.open(block, location, isopen) {
	block.setOpen(block, isopen);
	//sound
	if(isopen) {
		if(block.hasTag(block, $doors_tag)) {
			sound.spawn(location, $open_door_sound, $block_sound_category);
		}
	} else {
		if(block.hasTag(block, $doors_tag)) {
			sound.spawn(location, $close_door_sound, $block_sound_category);
		}
	}
}

function block.isDoor(block) {
	return block.hasTag(block, $doors_tag);
}

function block.isTrapdoor(block) {
	return block.hasTag(block, $trapdoors_tag);
}