//---map specific---
creative_world = world.get("creative");
edge1 = loc.new(creative_world, 2991, 54, 2956);
edge2 = loc.new(creative_world, 3009, 72, 2974);

x1 = math.min(loc.getX(edge1), loc.getX(edge2));
x2 = math.max(loc.getX(edge1), loc.getX(edge2));
y1 = math.min(loc.getY(edge1), loc.getY(edge2));
y2 = math.max(loc.getY(edge1), loc.getY(edge2));
z1 = math.min(loc.getZ(edge1), loc.getZ(edge2));
z2 = math.max(loc.getZ(edge1), loc.getZ(edge2));

mid_x = (x1+x2)/2;
mid_y = (y1+y2)/2;
mid_z = (z1+z2)/2;

//------------------

air_material = material.get("AIR");
stone_material = material.get("STONE");

box_iterator = loc.iterator(creative_world, x1,y1,z1,x2,y2,z2);

grav_down = list.new();

while(hasnext(box_iterator)){
	temp_block = block.get(next(box_iterator));
	if(block.getType(temp_block) != air_material){
		list.add(grav_down, temp_block);
	}
}

//grav_down
grav_up = list.new();
grav_north = list.new();
grav_south = list.new();
grav_west = list.new();
grav_east = list.new();

box.create_rotations();

active_grav = "down";

possible_names_map = list.new();
list.add(possible_names_map, "down");
list.add(possible_names_map, "up");
list.add(possible_names_map, "north");
list.add(possible_names_map, "south");
list.add(possible_names_map, "west");
list.add(possible_names_map, "east");


//Playerinv
uuid = "6cc9f8c7-9dfd-44f4-a3f2-af30054411a8";
player = player.get(uuid);
player_inv = player.getInv(player);

item_down = item.new(material.get("purple_concrete"),1);
item.setName(item_down, text.new("down"));
item_up = item.new(material.get("yellow_concrete"),1);
item.setName(item_up, text.new("up"));
item_north = item.new(material.get("pink_concrete"),1);
item.setName(item_north, text.new("north"));
item_south = item.new(material.get("magenta_concrete"),1);
item.setName(item_south, text.new("south"));
item_west = item.new(material.get("red_concrete"),1);
item.setName(item_west, text.new("west"));
item_east = item.new(material.get("blue_concrete"),1);
item.setName(item_east, text.new("east"));

inv.setItem(player_inv, 0, item_down);
inv.setItem(player_inv, 1, item_up);
inv.setItem(player_inv, 2, item_north);
inv.setItem(player_inv, 3, item_south);
inv.setItem(player_inv, 4, item_west);
inv.setItem(player_inv, 5, item_east);

//---events---

event.load("block_click");
//event.load("entity_damage");
//event.load("player_move");

//------------

@wait
wait();
if(event == "block_click"){
	if(player.getName(player) != "SirTerence7"){
		goto("wait");
	}
	if(action == "PHYSICAL"){
		goto("wait");
	}
	player_hand = living.getHand(player);
	if(!item.hasName(player_hand)){
		goto("wait");
	}
	item_name = string.text(item.getName(player_hand));
	if(!list.contains(possible_names_map, item_name)){
		goto("wait");
	}
	active_grav = box.rotate(active_grav, item_name);
}
goto("wait");

//---functions---

function box.create_rotations(){
	list_iterator = iterator($grav_down);
	while(hasnext(list_iterator)){
		temp_block = next(list_iterator);
		temp_loc = block.getLocation(temp_block);
		list.add($grav_up, block.get(box.rotate_location(temp_loc, "down", "up")));
		list.add($grav_north, block.get(box.rotate_location(temp_loc, "down", "north")));
		list.add($grav_south, block.get(box.rotate_location(temp_loc, "down", "south")));
		list.add($grav_west, block.get(box.rotate_location(temp_loc, "down", "west")));
		list.add($grav_east, block.get(box.rotate_location(temp_loc, "down", "east")));
	}
}

function box.rotate(from_rotation, to_rotation){
	box.clear(from_rotation);
	box.place(to_rotation);
	player_loc = entity.getLocation($player);
	teleport_location = box.rotate_location(player_loc, from_rotation, to_rotation);
	entity.teleport($player, teleport_location);
	return to_rotation;
}

function box.clear(rotation){
	if(rotation == "down"){
		list_iterator = iterator($grav_down);
	}elseif(rotation == "up"){
		list_iterator = iterator($grav_up);
	}elseif(rotation == "north"){
		list_iterator = iterator($grav_north);
	}elseif(rotation == "south"){
		list_iterator = iterator($grav_south);
	}elseif(rotation == "west"){
		list_iterator = iterator($grav_west);
	}elseif(rotation == "east"){
		list_iterator = iterator($grav_east);
	}
	while(hasnext(list_iterator)){
		block.setMaterial(next(list_iterator), $air_material, false);
	}
}

function box.place(rotation){
	if(rotation == "down"){
		list_iterator = iterator($grav_down);
	}elseif(rotation == "up"){
		list_iterator = iterator($grav_up);
	}elseif(rotation == "north"){
		list_iterator = iterator($grav_north);
	}elseif(rotation == "south"){
		list_iterator = iterator($grav_south);
	}elseif(rotation == "west"){
		list_iterator = iterator($grav_west);
	}elseif(rotation == "east"){
		list_iterator = iterator($grav_east);
	}
	while(hasnext(list_iterator)){
		temp_block = next(list_iterator);
		block.setMaterial(temp_block, $stone_material);//block.getType(temp_block));
	}
}

function box.rotate_location(abs_loc, from_rotation, to_rotation){
	/*
		rotation can be: down (default), up, north, south, west, east
	*/
	rel_loc = loc.mod(abs_loc, -$mid_x, -$mid_y, -$mid_z);
	rel_X = loc.getX(rel_loc);
	rel_Y = loc.getY(rel_loc);
	rel_Z = loc.getZ(rel_loc);
	
	if(from_rotation == to_rotation){
		return abs_loc;
	}
	
	if(from_rotation == "down"){
		from_loc_X = rel_X;
		from_loc_Y = rel_Y;
		from_loc_Z = rel_Z;
	}elseif(from_rotation == "up"){
		from_loc_X = rel_X;
		from_loc_Y = -rel_Y;
		from_loc_Z = -rel_Z;
	}elseif(from_rotation == "north"){
		from_loc_X = rel_X;
		from_loc_Y = -rel_Z;
		from_loc_Z = rel_Y;
	}elseif(from_rotation == "south"){
		from_loc_X = rel_X;
		from_loc_Y = rel_Z;
		from_loc_Z = -rel_Y;
	}elseif(from_rotation == "west"){
		from_loc_X = -rel_Y;
		from_loc_Y = rel_X;
		from_loc_Z = rel_Z;
	}elseif(from_rotation == "east"){
		from_loc_X = rel_Y;
		from_loc_Y = -rel_X;
		from_loc_Z = rel_Z;
	}
	
	if(to_rotation == "down"){
		to_loc_X = from_loc_X;
		to_loc_Y = from_loc_Y;
		to_loc_Z = from_loc_Z;
	}elseif(to_rotation == "up"){
		to_loc_X = from_loc_X;
		to_loc_Y = -from_loc_Y;
		to_loc_Z = -from_loc_Z;
	}elseif(to_rotation == "north"){
		to_loc_X = from_loc_X;
		to_loc_Y = -from_loc_Z;
		to_loc_Z = from_loc_Y;
	}elseif(to_rotation == "south"){
		to_loc_X = from_loc_X;
		to_loc_Y = from_loc_Z;
		to_loc_Z = -from_loc_Y;
	}elseif(to_rotation == "west"){
		to_loc_X = -from_loc_Y;
		to_loc_Y = from_loc_X;
		to_loc_Z = from_loc_Z;
	}elseif(to_rotation == "east"){
		to_loc_X = from_loc_Y;
		to_loc_Y = -from_loc_X;
		to_loc_Z = from_loc_Z;
	}
	
	loc.setX(abs_loc, to_loc_X + $mid_x);
	loc.setY(abs_loc, to_loc_Y + $mid_y);
	loc.setZ(abs_loc, to_loc_Z + $mid_z);
	return abs_loc;
}