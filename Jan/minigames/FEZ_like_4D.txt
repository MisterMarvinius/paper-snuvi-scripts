sign.started(gamesignloc);
event.load("block_click");
event.load("entity_damage");
event.load("player_move");
event.load("player_quit");
event.load("player_giveup");
event.load("minigame_join");
event.load("block_break");

games = world.getGames();

gamename = "ยง4D-FEZ";

maxplayers = 1;

air_material = material.get("AIR");

x1 = loc.getX(Edge_1);
x2 = loc.getX(Edge_2);
x3 = loc.getX(Edge_3);
x4 = x3 + x2 - x1;
Spawn_X = loc.getX(Start_Pos)-0.5;

y1 = loc.getY(Edge_1);
y2 = loc.getY(Edge_2);
y3 = loc.getY(Edge_3);
y4 = y3 + y2 - y1;
Spawn_Y = loc.getY(Start_Pos);

z1 = loc.getZ(Edge_1);
z2 = loc.getZ(Edge_2);
z3 = loc.getZ(Edge_3);
z4 = z3 + z2 - z1;
Spawn_Z = loc.getZ(Start_Pos)-0.5;

//XYZ -> WYZ -> XYW -> XYZ
Dimension_Blocks = list.new();

event.addMoveData(loc1, loc2, -1, -1);

currently_active_dimensions = 0; //0 = XYZ, 1 = WYZ, 2 = WYX, 3 = ZYX, 4 = ZYW, 5 = XYW
difference_array = array.new(6);
difference_array[0] = Layer_amount;
difference_array[1] = x2-x1;
difference_array[2] = z2-z2;
difference_array[3] = Layer_amount;
difference_array[4] = x2-x1;
difference_array[5] = z2-z2;

@wait
wait();
if(event == "entity_damage") {
	if(!isPlayer(entity)) {
		goto("wait");
	}
	player = entity;
}

if(!player.hasMinigameId(player, script_id)) {
	goto("wait");
}

if(event == "minigame_join"){
	msg.prefix(player, $gamename, "Game is Loading, please be patient.");
	entity.teleport(player, Start_Pos);
	player.clearInventory(player);
	player_inv = player.getInv(player);
	waitfor(10);
	remove_blocks();
	$currently_active_dimensions = 0;
	$current_layer[$currently_active_dimensions] = 0;
	inv.setItem(player_inv, 0, item.custom.create("ARROW_LEFT",1,"Turn down","Interdimensionally"));
	inv.setItem(player_inv, 1, item.custom.create("ARROW_RIGHT",1,"Turn up","Interdimensionally"));
	inv.setItem(player_inv, 2, item.custom.create("ARROW_DOWN",1,"Move down","background axis"));
	inv.setItem(player_inv, 3, item.custom.create("ARROW_UP",1,"Move up","background axis"));
	c = 0;
	for(layer_var = 0; layer_var < Layer_amount; layer_var++){
		x_d = layer_var * Layer_difference_X;
		y_d = layer_var * Layer_difference_Y;
		z_d = layer_var * Layer_difference_Z;
		for(x = x1 - x_d; x <= x2 - x_d; x++){
			for(y = y1 - y_d; y <= y2 - y_d; y++){
				for(z = z1 - z_d; z <= z2 - z_d; z++){
					c++;
					if(c > 500){
						c = 0;
						waitfor(2);
					}
					temp_block = block.get(loc.new(games, x, y, z));
					if(!block.isAir(temp_block)){
						D_Array = array.new(4);
						D_Array[0] = x - x1 + x_d;//X
						D_Array[1] = y - y1 + y_d;//Y
						D_Array[2] = z - z1 + z_d;//Z
						D_Array[3] = layer_var;//W
						temp_array = array.new(2);
						temp_array[0] = D_Array;
						temp_array[1] = block.getType(temp_block);
						list.add(Dimension_Blocks, temp_array);//Dimensional_Blocks -> Koords4D,Block
					}
				}
			}
		}
	}
	Active_blocks = place_blocks(Dimension_Blocks);
	msg.prefix(player, $gamename, "Game has loaded.");
	//snuvi.debug(list.getSize(Dimension_Blocks));
	goto("wait");
}

if(event == "block_break"){
	cancel = true;
	goto("wait");
}

if(event == "entity_damage") {
	cancel = true;
	goto("wait");
}

if(event == "block_click"){
	if(slot.isoffhand(hand)){
		goto("wait");
	}
	if(action == "PHYSICAL"){
		goto("wait");
	}
	player_hand = living.getHand(player);
	if(!item.hasName(player_hand)){
		goto("wait");
	}
	item_name = string.text(item.getName(player_hand));
	if(item_name == "Turn down"){
		goto("dimensional_turn_down");
	}
	if(item_name == "Turn up"){
		goto("dimensional_turn_up");
	}
	goto("wait");
}

if(event == "player_move"){
	//if(current_layer[1] == finish_layer){
	game_stop("You did it!");
	//}
	goto("wait");
}

if(event == "player_quit"){
	game_stop(null);
}

if(event == "player_giveup"){
	game_stop("You left :(");
}
goto("wait");

@dimensional_turn_up
remove_blocks();//0 = XYZ, 1 = WYZ, 2 = WYX, 3 = ZYX, 4 = ZYW, 5 = XYW
temp_layer = current_layer[currently_active_dimensions];
temp_player_loc = entity.getLocation(player);
stood_on_block_loc = loc.new(loc.getWorld(temp_player_loc), math.round(loc.getX(temp_player_loc)), math.round(loc.getY(temp_player_loc)-1), math.round(loc.getZ(temp_player_loc)));
if(block.getType == air_material){
	goto("wait");
}
if(y3 >= loc.getX(stood_on_block_loc)){
	goto("wait");
}
if(map.contains(Active_blocks, stood_on_block_loc)){
	temp_layer = map.get(Active_blocks, stood_on_block_loc);
}
else{
	goto("wait");
}

if(currently_active_dimensions == 0){//XYZ to WYZ
	currently_active_dimensions++;//0 = W, 1 = X, 2 = Z
	entity.teleport(player, loc.mod(Edge_3, temp_layer+0.5, loc.getY(temp_player_loc)-Spawn_Y, loc.getZ(temp_player_loc)-Spawn_Z));
}elseif(currently_active_dimensions == 1){//WYZ to WYX
	currently_active_dimensions++;
	entity.teleport(player, loc.mod(Edge_3, loc.getX(temp_player_loc)-Spawn_X, loc.getY(temp_player_loc)-Spawn_Y, temp_layer+0.5));
}elseif(currently_active_dimensions == 2){//WYX to ZYX
	currently_active_dimensions++;
	entity.teleport(player, loc.mod(Edge_3, temp_layer+0.5, loc.getY(temp_player_loc)-Spawn_Y, loc.getZ(temp_player_loc)-Spawn_Z));
}elseif(currently_active_dimensions == 3){//ZYX to ZYW
	currently_active_dimensions++;
	entity.teleport(player, loc.mod(Edge_3, loc.getX(temp_player_loc)-Spawn_X, loc.getY(temp_player_loc)-Spawn_Y, temp_layer+0.5));
}elseif(currently_active_dimensions == 4){//ZYW to XYW
	currently_active_dimensions++;
	entity.teleport(player, loc.mod(Edge_3, temp_layer+0.5, loc.getY(temp_player_loc)-Spawn_Y, loc.getZ(temp_player_loc)-Spawn_Z));
}elseif(currently_active_dimensions == 5){//XYW to XYZ
	currently_active_dimensions = 0;
	entity.teleport(player, loc.mod(Edge_3, loc.getX(temp_player_loc)-Spawn_X, loc.getY(temp_player_loc)-Spawn_Y, temp_layer+0.5));
}
Active_blocks = place_blocks(Dimension_Blocks);
goto("wait");

@dimensional_turn_down
remove_blocks();//0 = XYZ, 1 = WYZ, 2 = WYX, 3 = ZYX, 4 = ZYW, 5 = XYW
temp_player_loc = entity.getLocation(player);
stood_on_block_loc = loc.new(loc.getWorld(temp_player_loc), math.round(loc.getX(temp_player_loc)), math.round(loc.getY(temp_player_loc)-1), math.round(loc.getZ(temp_player_loc)));
if(block.getType == air_material){
	goto("wait");
}
if(y3 >= loc.getX(stood_on_block_loc)){
	goto("wait");
}
if(map.contains(Active_blocks, stood_on_block_loc)){
	temp_layer = map.get(Active_blocks, stood_on_block_loc);
}
else{
	goto("wait");
}

if(currently_active_dimensions == 0){//XYZ to XYW
	currently_active_dimensions = 5;//0 = W, 1 = X, 2 = Z, 3 = W, 4 = X, 5 = Z
	entity.teleport(player, loc.mod(Edge_3, loc.getX(temp_player_loc)-Spawn_X, loc.getY(temp_player_loc)-Spawn_Y, temp_layer+0.5));
}elseif(currently_active_dimensions == 1){//WYZ to XYZ
	currently_active_dimensions--;
	entity.teleport(player, loc.mod(Edge_3, temp_layer+0.5, loc.getY(temp_player_loc)-Spawn_Y, loc.getZ(temp_player_loc)-Spawn_Z));
}elseif(currently_active_dimensions == 2){//WYX to WYZ
	currently_active_dimensions--;
	entity.teleport(player, loc.mod(Edge_3, loc.getX(temp_player_loc)-Spawn_X, loc.getY(temp_player_loc)-Spawn_Y, temp_layer+0.5));
}elseif(currently_active_dimensions == 3){//ZYX to WYX
	currently_active_dimensions--;
	entity.teleport(player, loc.mod(Edge_3, temp_layer+0.5, loc.getY(temp_player_loc)-Spawn_Y, loc.getZ(temp_player_loc)-Spawn_Z));
}elseif(currently_active_dimensions == 4){//ZYW to ZYX
	currently_active_dimensions--;
	entity.teleport(player, loc.mod(Edge_3, loc.getX(temp_player_loc)-Spawn_X, loc.getY(temp_player_loc)-Spawn_Y, temp_layer+0.5));
}elseif(currently_active_dimensions == 5){//XYW to ZYW
	currently_active_dimensions--;
	entity.teleport(player, loc.mod(Edge_3, temp_layer+0.5, loc.getY(temp_player_loc)-Spawn_Y, loc.getZ(temp_player_loc)-Spawn_Z));
}
Active_blocks = place_blocks(Dimension_Blocks);
goto("wait");



function remove_blocks(){
	c = 0;
	for(x = $x3; x < $x3 + $x2 - $x1 + 1; x++){
		for(y = $y3; y < $y3 + $y2 - $y1 + 1; y++){
			for(z = $z3; z < $z3 + $z2 - $z1 + 1; z++){
				c++;
				if(c > 750){
					c = 0;
					waitfor(2);
				}
				block.setmaterial(block.get(loc.new($games, x, y, z)), $air_material);
			}
		}
	}
}

function place_blocks(Dimension_Blocks){
	c = 0;
	temp_a_d = $currently_active_dimensions;
	active_blocks = map.new();
	for(a = 0; a < list.getSize(Dimension_Blocks); a++){
		c++;
		if(c > 750){
			c = 0;
			waitfor(2);
		}
		temp_array = list.getIndex(Dimension_Blocks, a);
		D_Array = temp_array[0];//temp_array[1] ist der Block-Type, bei remove egal. D_Array[0] = x, D_Array[1] = y, D_Array[2] = z, D_Array[3] = w
		//0 = XYZ, 1 = WYZ, 2 = XYW
		//x3+x-x1+x_d, y3+y-y1+y_d, z3+z-z1+z_d
		X_place = 0;
		Y_place = -1;
		Z_place = 0;
		if(temp_a_d == 0){//0 = XYZ
			X_place = D_Array[0];
			Y_place = D_Array[1];
			Z_place = D_Array[2];
			background_coordinate = D_Array[3];
		}elseif(temp_a_d == 1){//1 = WYZ
			X_place = D_Array[3];
			Y_place = D_Array[1];
			Z_place = D_Array[2];
			background_coordinate = D_Array[0];
		}elseif(temp_a_d == 2){//2 = WYX
			X_place = D_Array[3];
			Y_place = D_Array[1];
			Z_place = D_Array[0];
			background_coordinate = D_Array[2];
		}elseif(temp_a_d == 3){//3 = ZYX
			X_place = D_Array[2];
			Y_place = D_Array[1];
			Z_place = D_Array[0];
			background_coordinate = D_Array[3];
		}elseif(temp_a_d == 4){//4 = ZYW
			X_place = D_Array[2];
			Y_place = D_Array[1];
			Z_place = D_Array[3];
			background_coordinate = D_Array[0];
		}elseif(temp_a_d == 5){//5 = XYW
			X_place = D_Array[0];
			Y_place = D_Array[1];
			Z_place = D_Array[3];
			background_coordinate = D_Array[2];
		}
		if(Y_place > -1){
			X_place = X_place + $x3;
			Y_place = Y_place + $y3;
			Z_place = Z_place + $z3;
			if(X_place < $x4+1 && Y_place < $y4+1 && Z_place < $z4+1){
				temp_loc = loc.new($games,X_place,Y_place,Z_place);
				block.setmaterial(block.get(temp_loc), temp_array[1]);
				map.add(active_blocks, temp_loc, background_coordinate)
			}
		}
	}
	return(active_blocks);
}

function game_stop(message){
	if(message != null){
		msg.prefix($player, $gamename, message);
	}
	script = script.getFromId($script_id);
	minigame.kickPlayer(script, $player);
	minigame.term(script, $gamesignloc);
	term();
}